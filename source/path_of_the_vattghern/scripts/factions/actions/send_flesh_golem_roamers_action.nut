this.send_flesh_golem_roamers_action <- this.inherit("scripts/factions/faction_action", {
	m = {},
	function create()
	{
		this.m.ID = "send_flesh_golem_roamers_action";
		// Default 30
		this.m.Cooldown = 35.0;
		this.m.IsSettlementsRequired = true;
		this.faction_action.create();
	}

	function onUpdate( _faction )
	{
		// Also a city encounter that fires after this time has passed
		// Flavor event: Flesh Constructs roam the lands!
		if (this.World.getTime().Days < 15)
		{
			return;
		}

		local settlements = _faction.getSettlements();

		// default 6
		if (settlements.len() < 6)
		{
			return;
		}

		//default 3
		if (_faction.getUnits().len() >= 4)
		{
			return;
		}

		local allowed = false;

		foreach( s in _faction.getSettlements() )
		{
			if (s.getLastSpawnTime() + 300.0 > this.Time.getVirtualTimeF())
			{
				continue;
			}

			allowed = true;
			break;
		}

		if (!allowed)
		{
			return;
		}

		// default 10
		this.m.Score = 14;
	}

	function onClear()
	{
	}

	function onExecute( _faction )
	{
		local settlements = [];

		foreach( s in _faction.getSettlements() )
		{
			if (s.getRoamerSpawnList() == null)
			{
				continue;
			}

			if (s.getLastSpawnTime() + 300.0 > this.Time.getVirtualTimeF())
			{
				continue;
			}

			settlements.push({
				D = s,
				P = 10
			});
		}

		if (settlements.len() == 0)
		{
			return;
		}

		local settlement = this.pickWeightedRandom(settlements);
		settlement.setLastSpawnTimeToNow();
		local party = this.getFaction().spawnEntity(settlement.getTile(), "Flesh Golems", false, ::Const.World.Spawn.PovFleshGolems, this.Math.max(settlement.getResources() * 0.75, this.Math.rand(60, 100) * this.getReputationToDifficultyLightMult()));
		party.getSprite("banner").setBrush("pov_banner_flesh");
		party.setDescription("Foul Flesh Golems shambling around, directed to look for new victims.");
		party.setFootprintType(this.Const.World.FootprintsType.Undead);
		party.setSlowerAtNight(false);
		party.setUsingGlobalVision(false);
		party.setLooting(false);
		party.getFlags().set("IsRandomlySpawned", true);
		party.getLoot().Money = this.Math.rand(0, 200);
		party.getLoot().ArmorParts = this.Math.rand(0, 6);
		party.getLoot().Medicine = this.Math.rand(3, 9);
		local r = this.Math.rand(1, 2);
		local s = this.Math.rand(1, 100);

		if (r == 1)
		{
			party.addToInventory("loot/signet_ring_item");
		}

		if (r == 2)
		{
			party.addToInventory("supplies/strange_meat_item");
		}

		if (s <= 25)
		{
			party.addToInventory("misc/pov_distilled_alcohol_item");
		}

		local c = party.getController();
		local roam = this.new("scripts/ai/world/orders/roam_order");
		roam.setAllTerrainAvailable();
		roam.setTerrain(this.Const.World.TerrainType.Ocean, false);
		roam.setTerrain(this.Const.World.TerrainType.Mountains, false);
		roam.setPivot(settlement);
		roam.setAvoidHeat(true);
		roam.setTime(this.World.getTime().SecondsPerDay * 2);
		roam.setMinRange(1);
		roam.setMaxRange(7);
		local move = this.new("scripts/ai/world/orders/move_order");
		move.setDestination(settlement.getTile());
		local despawn = this.new("scripts/ai/world/orders/despawn_order");
		c.addOrder(roam);
		c.addOrder(move);
		c.addOrder(despawn);
	}

});

