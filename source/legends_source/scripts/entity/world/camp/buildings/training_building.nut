this.training_building <- this.inherit("scripts/entity/world/camp/camp_building", {
	m = {
		Results = [],
		NumBros = 0,
		UnTrained = 0,
		BaseCraft = 0.15, // was 1.0, changed tp 0.4 6/11/21 - Luft - dropped to 0.15 by poss 7/3/2023
		TrainingDescriptors = {
			M = ["Short Guard ", "Upper Snake Guard ", "Bastard Cross ", "The Middle Iron Door ", "thrusts ", "trips ", "grapples ", "foot passing ", "striking ", "vambrace traps ", "a pommel bash ", "half sword ", "The Thumb Scissor ", "jabs ", "hand to hand combat "],
    		T = ["for hours, ", "all day, ", "for several hours, ", "until exhaustion, ", "as long as possible, "],
     		A = [" feels ready for a fight", " needs a real opponent", " is prepared for battle", " is keen to try it out", " is ready for a scrap"],
		}
	},
	function create()
	{
		this.camp_building.create();
		this.m.ID = this.Const.World.CampBuildings.Training;
		this.m.ModName = "Training";
		this.m.Slot = "train";
		this.m.Name = "Training Grounds";
		this.m.Description = "Training";
		this.m.BannerImage = "ui/buttons/banner_train.png";
		this.m.CanEnter = false;
		this.m.Sounds = [
			{
				File = "ambience/camp/camp_training_01.wav",
				Volume = 1.0,
				Pitch = 1.0
			},
			{
				File = "ambience/camp/camp_training_02.wav",
				Volume = 1.0,
				Pitch = 1.0
			},
			{
				File = "ambience/camp/camp_training_03.wav",
				Volume = 1.0,
				Pitch = 1.0
			}
		];
		this.m.SoundsAtNight = [
			{
				File = "ambience/camp/camp_training_01.wav",
				Volume = 1.0,
				Pitch = 1.0
			},
			{
				File = "ambience/camp/camp_training_02.wav",
				Volume = 1.0,
				Pitch = 1.0
			},
			{
				File = "ambience/camp/camp_training_03.wav",
				Volume = 1.0,
				Pitch = 1.0
			}
		];
	}

	function getTitle()
	{
		if (this.getUpgraded())
		{
			return this.m.Name + " *Upgraded*";
		}

		return this.m.Name + " *Not Upgraded*";
	}

	function getDescription()
	{
		local desc = "";
		desc = desc + "Whether a seasoned veteran or a green recruit, there\'s always something new to learn. ";
		desc = desc + "Anyone assigned to train will gain experience over time based on the total modifier of occupiers in this tent. ";
		desc = desc + "Having highly skilled teachers in the grounds increases the chances of successfully learning something new, which will be tracked under \'Intensive Training\' progress under their traits. ";
		desc = desc + "There\'s always a slight chance someone can be injured.";
		desc = desc + "\n\n";
		desc = desc + "Training grounds can be upgraded by purchasing an upgrade set in local markets. Upgraded grounds reduce the ";
		desc = desc + "risk of accidents from a minimum of 5% to 1% and also give the chance of a permanent random skill increase.";
		return desc;
	}

	function getModifierToolip()
	{
		local mod = this.getModifiers();
		local ret = [
			{
				id = 6,
				type = "text",
				icon = "ui/buttons/asset_vision_up.png",
				text = "Total training modifier is [color=%positive%]" + mod.Craft * 100.0 + "%[/color]."
			}
		];
		local id = 7;

		foreach( bro in mod.Modifiers )
		{
			++id;
			local tooltip_text = "[color=%positive%]" + bro[0] * 100.0 + "%[/color] " + bro[1] + " (" + bro[2] + ")";

			if (bro[3])
			{
				tooltip_text = "[color=%positive%]" + bro[0] * 100.0 + "%[/color] " + bro[1] + " (" + bro[2] + ") [color=%negative%] Training fulfilled[/color]"
			}
			ret.push({
				id = id,
				type = "hint",
				icon = "ui/icons/special.png",
				text = tooltip_text
			});
		}

		return ret;
	}

	function isHidden()
	{
		if (::Legends.Settings.skipCamp())
		{
			return false;
		}

		return !this.World.Flags.get("HasLegendCampTraining");
	}

	function getUpgraded()
	{
		return this.Stash.hasItem(::Legends.Camp.Tent.Training);
	}

	function getLevel()
	{
		local pro = "dude";

		if (this.getUpgraded())
		{
			pro = "tent";
		}

		local sub = "empty";

		if (this.getAssignedBros() > 0)
		{
			sub = "full";
		}

		return pro + "_" + sub;
	}

	function init()
	{
		this.m.Results = [];
		this.m.NumBros = this.getAssignedBros();
		this.m.UnTrained = 0;
		local roster = this.World.getPlayerRoster().getAll();

		foreach( bro in roster )
		{
			if (bro.getCampAssignment() != this.m.ID)
			{
				continue;
			}

			++this.m.UnTrained;
		}
	}

	function getModifiers()
	{
		local ret = {
			Craft = 0.0,
			Assigned = 0,
			Modifiers = []
		};
		local roster = this.World.getPlayerRoster().getAll();
		local hasTrainer = false;

		foreach( bro in roster )
		{
			if (bro.getCampAssignment() == this.m.ID && bro.getSkills().hasPerk(::Legends.Perk.LegendMasterTrainer))
			{
				hasTrainer = true;
			}
		}


		foreach( bro in roster )
		{
			if (bro.getCampAssignment() != this.m.ID)
			{
				continue;
			}

			local mod = this.m.BaseCraft + this.m.BaseCraft * bro.getBackground().getModifiers().Training;

			if (bro.getSkills().hasPerk(::Legends.Perk.LegendBackToBasics))
			{
				mod = mod + 0.1;
			}

			if (hasTrainer)
			{
				mod += 0.1;
			}

			local max_reached = false;

			if (bro.getSkills().hasTrait(::Legends.Trait.LegendIntensiveTraining))
			{
				max_reached = ::Legends.Traits.get(bro, ::Legends.Trait.LegendIntensiveTraining).isMaxReached();
			}


			++ret.Assigned;
			ret.Modifiers.push([
				mod,
				bro.getName(),
				bro.getBackground().getNameOnly(),
				max_reached
			]);
		}

		ret.Modifiers.sort(this.sortModifiers);

		for( local i = 0; i < ret.Modifiers.len(); i = i )
		{
			ret.Modifiers[i][0] = ret.Modifiers[i][0] * this.Math.pow(i + 1, -0.5);

			if (this.getUpgraded())
			{
				ret.Modifiers[i][0] *= 1.15;
			}

			ret.Craft += ret.Modifiers[i][0];
			i = ++i;
		}

		return ret;
	}

	function getRandomBroName( broName, bros )
	{
		local names = [];

		foreach( b in bros )
		{
			if (b[1] == broName)
			{
				continue;
			}

			names.push(b[1]);
		}

		return names[this.Math.rand(0, names.len() - 1)];
	}


	function getResults()
	{
		local res = [];
		local id = 120;

		foreach( b in this.m.Results )
		{
			res.push({
				id = id,
				icon = b.Icon,
				text = b.Text
			});
			 ++id;

		}

		return res;
	}

	function getAssignedBros()
	{
		local mod = this.getModifiers();
		return mod.Assigned;
	}


	function getInjury( bro )
	{

		if (bro.getSkills().hasSkillOfType(this.Const.SkillType.TemporaryInjury) || bro.getSkills().hasSkillOfType(this.Const.SkillType.SemiInjury))
		{
			local injury = bro.addInjury(this.Const.Injury.Permanent);
			this.m.Results.push({
				Icon = injury.getIcon(),
				Text = bro.getName() + " suffers " + injury.getNameOnly() + " while training."
			});
		}
		else
		{
			local injury = bro.addInjury(this.Const.Injury.CampTraining);
			this.m.Results.push({
				Icon = injury.getIcon(),
				Text = bro.getName() + " suffers " + injury.getNameOnly() + " while training."
			});
		}

	}

	function getDescriptors( bro, extraTrainingDescriptors){
		local numberOfMTA = this.m.TrainingDescriptors.M.len() * this.m.TrainingDescriptors.T.len() * this.m.TrainingDescriptors.A.len();
		local numberOfExtraDescriptors = extraTrainingDescriptors.len() + numberOfMTA;
		local r = this.Math.rand(0, numberOfExtraDescriptors - 1);

		if (r < numberOfMTA) {
			return "After practicing " + this.m.TrainingDescriptors.M[(r / (this.m.TrainingDescriptors.A.len() * this.m.TrainingDescriptors.T.len())) % this.m.TrainingDescriptors.M.len()] + this.m.TrainingDescriptors.T[(r / this.m.TrainingDescriptors.A.len()) % this.m.TrainingDescriptors.T.len()] + bro.getName() + this.m.TrainingDescriptors.A[r % this.m.TrainingDescriptors.A.len()];
		}
		else {
			return bro.getName() + extraTrainingDescriptors[r - numberOfMTA];
		}
	}

	function getTrained( bro )
	{
		local inTraining = ::Legends.Traits.get(bro, ::Legends.Trait.LegendIntensiveTraining);
		local XPbonus = this.Math.floor(this.m.Camp.getCampTimeHours() * (this.getUpgraded() ? 10 : 5) * (inTraining == null ? 1 : (1 + inTraining.getBonusXP())));
		local originalXP = bro.m.XP;
		bro.addXP(XPbonus);
		bro.updateLevel();
		local extraTrainingDescriptors = [
			" learned a new move",
			" finally nails the fancy parry they have been been practicing",
			" invents an overly showy move",
			" finds a stance that feels more natural",
			" learns how to adjust the new armor so it fits better",
			" has perfected a new skill in practice",
			" had their ego bruised and wants an outlet for frustration",
			" left the training dummy as nothing more than a pile of splinters",
			" has practiced a skill so much it has become second nature",
			" feels more comfortable with their weapon",
			" sets a new personal best in training"
		];

		if (bro.getLevel() < 3)
		{
			extraTrainingDescriptors.push(" figures out what end of the weapon to hold");
			extraTrainingDescriptors.push(" remembers that you can move your legs as well as your arms");
		}

		this.m.Results.push({
			Icon = "ui/icons/xp_received.png",
			Text = this.getDescriptors(bro, extraTrainingDescriptors) + " and gains [color=" + this.Const.UI.Color.PositiveEventValue + "]" + (bro.m.XP - originalXP) + "[/color] XP."
		});
		return true;
	}

	function getTrainedAfter11( bro )
	{
		if (bro.getSkills().hasEffect(::Legends.Effect.Trained)) {
			return;
		}
		local effect = ::Legends.Effects.grant(bro, ::Legends.Effect.Trained, function(_effect) {
			_effect.m.Duration = 1;
			_effect.m.XPGainMult = 1.1;
			_effect.m.Icon = "skills/status_effect_75.png";
		}.bindenv(this));
		local extraTrainingDescriptors = [
			" learned how to get most of the next battle",
			" finds a stance that can improve his experience in the next battle",
			" is ready to learn more in the next battle"
		];

		this.m.Results.push({
			Icon = effect.getIcon(),
			Text = this.getDescriptors(bro, extraTrainingDescriptors) + " and gains a [color=" + this.Const.UI.Color.PositiveEventValue + "]10%[/color] xp increase for the next battle."
		});
		return true;
	}

	function getFailed( _bro )
	{
		this.m.Results.push({
			Icon = "ui/tooltips/negative.png", //Should get an icon for failed training
			Text = _bro.getName() + " didn't learn anything useful."
		});
	}

	function getBreak( _bro )
	{
		this.m.Results.push({
			Icon = "ui/icons/days_wounded.png", //Should get an icon for failed training
			Text = _bro.getName() + " was recovering from an injury so didn't train."
		});
	}

	function getBonus( bro )
	{
		if (!bro.getSkills().hasTrait(::Legends.Trait.LegendIntensiveTraining))
		{
			return;
		}

		local inTraining = ::Legends.Traits.get(bro, ::Legends.Trait.LegendIntensiveTraining);

		if (inTraining.isMaxReached())
		{
			return;
		}

		local text = "";
		local icon = "";
		local attr = this.Math.rand(0, this.Const.Attributes.COUNT - 1);

		switch(attr)
		{
		case 0:
			bro.getBaseProperties().Hitpoints += 1;
			icon = "ui/icons/health.png";
			text = "Hitpoint";
			inTraining.addHitpoint();
			break;

		case 1:
			bro.getBaseProperties().Bravery += 1;
			icon = "ui/icons/bravery.png";
			text = "Resolve";
			inTraining.addBrave();
			break;

		case 2:
			bro.getBaseProperties().Stamina += 1;
			icon = "ui/icons/fatigue.png";
			text = "Fatigue";
			inTraining.addStamina();
			break;

		case 3:
			bro.getBaseProperties().Initiative += 1;
			icon = "ui/icons/initiative.png";
			text = "Initiative";
			inTraining.addIni();
			break;

		case 4:
			if (bro.getBaseProperties().MeleeSkill > bro.getBaseProperties().RangedSkill)
			{
				bro.getBaseProperties().MeleeSkill += 1;
				icon = "ui/icons/melee_skill.png";
				text = "Melee Skill";
				inTraining.addMatk();
			}
			else
			{
				bro.getBaseProperties().RangedSkill += 1;
				icon = "ui/icons/ranged_skill.png";
				text = "Ranged Skill";
				inTraining.addRatk();
			}

			break;

		case 5:
			bro.getBaseProperties().MeleeDefense += 1;
			icon = "ui/icons/melee_defense.png";
			text = "Melee Defense";
			inTraining.addMdef();
			break;

		default:
			bro.getBaseProperties().RangedDefense += 1;
			icon = "ui/icons/ranged_defense.png";
			text = "Ranged Defense";
			inTraining.addRdef();
			break;
		}

		bro.getSkills().update();

		if (inTraining.isMaxReached())
		{
			bro.m.PerkPoints += 1;
			icon = "ui/icons/level.png";
			local traitConst = ::Legends.Training.addRandomTrainingTrait(bro);
			inTraining.finishedTraining(traitConst);
			local trait = ::Legends.Traits.get(bro, traitConst);
			this.m.Results.push({
				Icon = icon,
				Text = bro.getName() + " completed the training course and gains [color=" + this.Const.UI.Color.PositiveEventValue + "]1[/color] " + text + ", Perk Point and " + trait.getName()
			});
		}
		else
		{
			this.m.Results.push({
				Icon = icon,
				Text = bro.getName() + " had a breakthrough training session and gains [color=" + this.Const.UI.Color.PositiveEventValue + "]1[/color] " + text
			});
		}
	}

	function getUpdateText()
	{
		if (this.m.NumBros == 0)
		{
			return null;
		}

		if (this.getUpgraded())
		{
			return "Training ... " + this.m.NumBros + " brothers";
		}

		return "Training ... " + this.m.UnTrained + " / " + this.m.NumBros + " brothers";
	}

	function completed()
	{
		local roster = this.World.getPlayerRoster().getAll();

		foreach( bro in roster )
		{
			if (bro.getCampAssignment() != this.m.ID)
			{
				continue;
			}

			if (bro.getSkills().hasSkillOfType(this.Const.SkillType.TemporaryInjury) || bro.getSkills().hasSkillOfType(this.Const.SkillType.SemiInjury))
			{
				this.getBreak(bro);
				continue;
			}

			local r = this.Math.min(95, 100 * this.Math.pow(this.m.Camp.getCampTimeHours() / 12.0, 0.6 + 0.1 * bro.getLevel()));

			if ( this.Math.rand(1, 100) < r)
			{
				if ( bro.getLevel() < 12 )
				{
					this.getTrained(bro);
				}
				else
				{
					this.getTrainedAfter11(bro);
				}
			}
			else
			{
				this.getFailed(bro);
			}

			local injuryMin = 5;

			if (this.getUpgraded())
			{
				injuryMin = 1;
				local mod = this.getModifiers();

				for( local camphrs = this.m.Camp.getCampTimeHours(); camphrs > 0;  )
				{
					local r = this.Math.rand(1, 100);

					if (r <= camphrs + mod.Craft * camphrs)
					{
						this.getBonus(bro);
						camphrs = camphrs - r;
					}
					else
					{
						break;
					}
				}
			}

			local r = this.Math.min(injuryMin, 4 * this.Math.pow(this.m.Camp.getCampTimeHours(), 0.5) - bro.getLevel());

			if (this.Math.rand(1, 100) < r)
			{
				this.getInjury(bro);
			}

			if (this.Math.rand(1, 100) < r)
			{
				::Legends.Effects.grant(bro, ::Legends.Effect.Exhausted);
			}

		}
	}

	function onClicked( _campScreen )
	{
		_campScreen.showTrainingDialog();
		this.camp_building.onClicked(_campScreen);
	}

	function onSerialize( _out )
	{
		this.camp_building.onSerialize(_out);
	}

	function onDeserialize( _in )
	{
		this.camp_building.onDeserialize(_in);
	}
});

