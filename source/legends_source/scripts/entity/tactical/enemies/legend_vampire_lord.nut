this.legend_vampire_lord <- this.inherit("scripts/entity/tactical/actor", {
	m = {
		WasInjured = false,
		IsLady = false
	},
	function create()
	{
		this.m.Type = this.Const.EntityType.LegendVampireLord;
		this.m.BloodType = this.Const.BloodType.Red;
		this.m.MoraleState = this.Const.MoraleState.Ignore;
		this.m.XP = this.Const.Tactical.Actor.LegendVampireLord.XP;
 		if(this.Math.rand(1, 100) <= 50)
		 {
			 this.m.IsLady = true;
		 }

		this.actor.create();
		this.m.Sound[this.Const.Sound.ActorEvent.DamageReceived] = [
			"sounds/enemies/vampire_hurt_01.wav",
			"sounds/enemies/vampire_hurt_02.wav",
			"sounds/enemies/vampire_hurt_03.wav"
		];
		this.m.Sound[this.Const.Sound.ActorEvent.Death] = [
			"sounds/enemies/vampire_death_01.wav",
			"sounds/enemies/vampire_death_02.wav",
			"sounds/enemies/vampire_death_03.wav"
		];
		this.m.Sound[this.Const.Sound.ActorEvent.Idle] = [
			"sounds/enemies/vampire_idle_01.wav",
			"sounds/enemies/vampire_idle_02.wav",
			"sounds/enemies/vampire_idle_03.wav"
		];
		this.getFlags().add("undead");
		this.getFlags().add("vampire");
		this.m.AIAgent = this.new("scripts/ai/tactical/agents/vampire_agent");
		this.m.AIAgent.setActor(this);

		this.m.OnDeathLootTable.extend([
			[100, "scripts/items/misc/vampire_dust_item"],
			[8, "scripts/items/misc/legend_masterwork_metal"],
			[7, "scripts/items/misc/legend_masterwork_tools"]
		]);
		local rolls = ::Legends.S.extraLootChance(1);
		for(local i = 0; i < rolls; i++) {
			this.m.OnDeathLootTable.extend([
				[3, "scripts/items/misc/legend_ancient_scroll_item"]
			]);
		}
	}

	function onDeath( _killer, _skill, _tile, _fatalityType )
	{
		if (!this.Tactical.State.isScenarioMode() && _killer != null && _killer.isPlayerControlled())
		{
			this.updateAchievement("BackToTheGrave", 1, 1);
		}

		this.m.BloodType = this.Const.BloodType.Ash;
		local flip = this.Math.rand(0, 100) < 50;
		this.m.IsCorpseFlipped = flip;

		if (_tile != null)
		{
			local decal = _tile.spawnDetail("bust_vampire_lord_dead", this.Const.Tactical.DetailFlag.Corpse, flip, false);
			decal.Scale = 0.9;
			decal.setBrightness(0.9);
			this.spawnTerrainDropdownEffect(_tile);
			this.spawnFlies(_tile);
		}

		local deathLoot = this.getItems().getDroppableLoot(_killer);
		local tileLoot = this.getLootForTile(_killer, deathLoot);
		local corpse = this.generateCorpse(_tile, _fatalityType, _killer);
		this.dropLoot(_tile, tileLoot, !flip);

		if (_tile == null) {
			this.Tactical.Entities.addUnplacedCorpse(corpse);
		} else {
			_tile.Properties.set("Corpse", corpse);
			this.Tactical.Entities.addCorpse(_tile);
		}

		this.actor.onDeath(_killer, _skill, _tile, _fatalityType);
	}

	function generateCorpse( _tile, _fatalityType, _killer )
	{
		local corpse = clone this.Const.Corpse;
		corpse.Faction = this.getFaction();
		corpse.CorpseName = "A " + this.getName();
		corpse.Armor = this.m.BaseProperties.Armor;
		corpse.Items = this.getItems().prepareItemsForCorpse(_killer);
		corpse.IsHeadAttached = true;
		corpse.IsConsumable = false;
		corpse.IsResurrectable = false;
		corpse.Tile = _tile;
		return corpse;
	}

	function onFactionChanged()
	{
		this.actor.onFactionChanged();
		local flip = !this.isAlliedWithPlayer();
		this.getSprite("body").setHorizontalFlipping(flip);
		this.getSprite("body_detail").setHorizontalFlipping(flip);
		this.getSprite("old_body").setHorizontalFlipping(flip);
		this.getSprite("armor").setHorizontalFlipping(flip);
		this.getSprite("head").setHorizontalFlipping(flip);
		this.getSprite("head_detail").setHorizontalFlipping(flip);
		this.getSprite("old_head").setHorizontalFlipping(flip);
		this.getSprite("injury").setHorizontalFlipping(flip);
		this.getSprite("beard").setHorizontalFlipping(flip);
		this.getSprite("hair").setHorizontalFlipping(flip);
		this.getSprite("helmet").setHorizontalFlipping(flip);
		this.getSprite("helmet_damage").setHorizontalFlipping(flip);
		//this.getSprite("beard_top").setHorizontalFlipping(flip);
		this.getSprite("body_blood").setHorizontalFlipping(flip);
		this.getSprite("dirt").setHorizontalFlipping(flip);
	}

	function onDamageReceived( _attacker, _skill, _hitInfo )
	{
		if (_hitInfo.DamageRegular >= 25)
		{
			this.m.WasInjured = true;
		}

		return this.actor.onDamageReceived(_attacker, _skill, _hitInfo);
	}

	function onUpdateInjuryLayer()
	{
		local p = this.getHitpointsPct();
		local bodyBrush = this.getSprite("body").getBrush().Name;
		local headBrush = this.getSprite("head").getBrush().Name;

		if (p <= 0.33)
		{
			this.getSprite("body").setBrush("bust_vampire_lord_body_03");
			this.getSprite("body_injury").setBrush("bust_skeleton_body_03_injured");
			this.getSprite("head").setBrush("bust_vampire_lord_head_03");
			this.getSprite("injury").setBrush("bust_skeleton_head_03_injured");
		}
		else if (p <= 0.66)
		{
			this.getSprite("body").setBrush("bust_vampire_lord_body_02");
			this.getSprite("body_injury").setBrush("bust_skeleton_body_04_injured");
			this.getSprite("head").setBrush("bust_vampire_lord_head_02");
			this.getSprite("injury").setBrush("bust_skeleton_head_04_injured");
		}
		else if (this.m.IsLady == true)
		{
			this.getSprite("body").setBrush("bust_vampire_lady_body_02");
			this.getSprite("body_injury").setBrush("bust_skeleton_body_05_injured");
			this.getSprite("head").setBrush("bust_vampire_lady_head_02");
			this.getSprite("injury").setBrush("bust_skeleton_head_05_injured");
		}
		else
		{
			this.getSprite("body").setBrush("bust_vampire_lord_body_01");
			this.getSprite("body_injury").setBrush("bust_skeleton_body_05_injured");
			this.getSprite("head").setBrush("bust_vampire_lord_head_01");
			this.getSprite("injury").setBrush("bust_skeleton_head_05_injured");
		}

		this.getSprite("body_injury").Visible = this.m.WasInjured;
		this.getSprite("injury").Visible = this.m.WasInjured;

		if (bodyBrush != this.getSprite("body").getBrush().Name)
		{
			local old_body = this.getSprite("old_body");
			old_body.Visible = true;
			old_body.Alpha = 255;
			old_body.setBrush(bodyBrush);
			old_body.fadeOutAndHide(3000);
			local old_head = this.getSprite("old_head");
			old_head.Visible = true;
			old_head.Alpha = 255;
			old_head.setBrush(headBrush);
			old_head.fadeOutAndHide(3000);
		}

		this.setDirty(true);
	}

	function onInit()
	{
		this.actor.onInit();
		local b = this.m.BaseProperties;
		b.setValues(this.Const.Tactical.Actor.Vampire);
		b.IsAffectedByNight = false;
		b.IsAffectedByInjuries = false;
		b.IsSpecializedInSwords = true;
		this.m.ActionPoints = b.ActionPoints;
		this.m.Hitpoints = b.Hitpoints;
		this.m.CurrentProperties = clone b;
		this.m.ActionPointCosts = this.Const.DefaultMovementAPCost;
		this.m.FatigueCosts = this.Const.DefaultMovementFatigueCost;
		local hairColor = this.Const.HairColors.Zombie[this.Math.rand(0, this.Const.HairColors.Zombie.len() - 1)];
		this.addSprite("socket").setBrush("bust_base_undead");
		local body = this.addSprite("body");
		body.setBrush("bust_vampire_lord_body_01");
		body.setHorizontalFlipping(true);
		this.addSprite("old_body");
		this.addSprite("body_injury").setBrush("bust_skeleton_body_05_injured");
		this.addSprite("armor");
		local head_detail = this.addSprite("head_detail");

		if (this.Math.rand(1, 100) <= 50)
		{
			this.m.IsLady ? head_detail.setBrush("bust_vampire_head_lady_detail_01") : head_detail.setBrush("bust_skeleton_head_detail_01");
		}

		local body_detail = this.addSprite("body_detail");
		if (this.Math.rand(1, 100) <= 75)
		{
			this.m.IsLady ? body_detail.setBrush("bust_vampire_lady_detail_0" + this.Math.rand(1, 2)) : body_detail.setBrush("bust_skeleton_detail_0" + this.Math.rand(2, 3));
		}
		local head = this.addSprite("head");
		head.setBrush("bust_vampire_lord_head_01");
		head.Color = body.Color;
		head.Saturation = body.Saturation;
		this.addSprite("old_head");
		local injury = this.addSprite("injury");
		injury.setBrush("bust_skeleton_head_05_injured");
		local beard = this.addSprite("beard");
		beard.setBrightness(0.7);
		beard.varyColor(0.02, 0.02, 0.02);
		local hair = this.addSprite("hair");
		hair.Color = beard.Color;
		if (this.Math.rand(1, 100) <= 75 && !this.m.IsLady)
		{
			local idx = this.Math.rand(0, this.Const.Hair.Vampire.len() - 1);
			hair.setBrush("hair_" + hairColor + "_" + this.Const.Hair.Vampire[idx]);
			this.setSpriteOffset("hair", this.createVec(0, -3));
		}
		else if (this.Math.rand(1, 100) <= 67 && this.m.IsLady)
		{
			local idx = this.Math.rand(0, this.Const.Hair.VampireLady.len() - 1);
			hair.setBrush("hair_" + hairColor + "_" + this.Const.Hair.VampireLady[idx]);
		}

		this.addSprite("helmet");
		this.addSprite("helmet_damage");

		if (this.m.IsLady)
		{
			this.getSprite("body").setBrush("bust_vampire_lady_body_02");
			this.getSprite("head").setBrush("bust_vampire_lady_head_02");
		}

		local body_blood = this.addSprite("body_blood");
		body_blood.setBrush("bust_body_bloodied_02");
		body_blood.setHorizontalFlipping(true);
		body_blood.Visible = false;
		local body_dirt = this.addSprite("dirt");
		body_dirt.setBrush("bust_body_dirt_02");
		body_dirt.setHorizontalFlipping(true);
		body_dirt.Visible = this.Math.rand(1, 100) <= 33;
		this.addDefaultStatusSprites();
		this.getSprite("status_rooted").Scale = 0.55;
		::Legends.Effects.grant(this, ::Legends.Effect.DoubleGrip);
		::Legends.Traits.grant(this, ::Legends.Trait.RacialVampire);
		::Legends.Actives.grant(this, ::Legends.Active.Darkflight);
		::Legends.Perks.grant(this, ::Legends.Perk.Anticipation);
		::Legends.Perks.grant(this, ::Legends.Perk.CripplingStrikes);
		::Legends.Perks.grant(this, ::Legends.Perk.HeadHunter);
		::Legends.Perks.grant(this, ::Legends.Perk.NineLives);
		::Legends.Perks.grant(this, ::Legends.Perk.LegendBloodbath);
		::Legends.Perks.grant(this, ::Legends.Perk.LegendCarnage);
		::Legends.Perks.grant(this, ::Legends.Perk.LegendPoisonImmunity);
		if(::Legends.isLegendaryDifficulty())
		{
			b.MeleeSkill += 10;
			::Legends.Perks.grant(this, ::Legends.Perk.LegendComposure);
			::Legends.Perks.grant(this, ::Legends.Perk.Fearsome);
			::Legends.Perks.grant(this, ::Legends.Perk.CoupDeGrace);
			::Legends.Perks.grant(this, ::Legends.Perk.Nimble);
			::Legends.Traits.grant(this, ::Legends.Trait.Fearless);
			::Legends.Perks.grant(this, ::Legends.Perk.LegendTerrifyingVisage);
		}

		::Legends.S.scaleBaseProperties(b);
	}

	function assignRandomEquipment()
	{
		this.getItems().equip(::Const.World.Common.pickItem([
			[1, "weapons/ancient/crypt_cleaver"],
			[1, "weapons/ancient/legend_great_khopesh"]
		], "scripts/items/"));

		// this.getItems().equip(::Const.World.Common.pickArmor([
		// 	[1, ::Legends.Armor.Standard.legend_vampire_lord_armor]
		// ]));
		// this.getItems().equip(::Const.World.Common.pickHelmet([
		// 	[1, ::Legends.Helmet.Standard.legend_vampire_lord_helmet]
		// ]));
	}

	function makeMiniboss()
	{
		if (!this.actor.makeMiniboss())
			return false;

		this.getSprite("miniboss").setBrush("bust_miniboss");

		this.getItems().equip(::Const.World.Common.pickItem([
			[1, "weapons/named/named_khopesh"],
			[2, "weapons/named/named_crypt_cleaver"]
		], "scripts/items/"));

		return true;
	}

});
