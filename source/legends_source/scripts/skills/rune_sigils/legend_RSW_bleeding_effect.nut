this.legend_RSW_bleeding_effect <- this.inherit("scripts/skills/skill", {
	m = {
		TurnsLeft = 2,
		Damage = 5,
		LastRoundApplied = 0,
		Actor = null
	},
	function setStats(_s1, _s2) {
		this.m.Damage = _s1;
		this.m.TurnsLeft = _s2;
	}
	function getDamage() {
		return this.m.Damage;
	}

	function setActor( _a ) {
		this.m.Actor = ::MSU.asWeakTableRef(_a);
	}

	function create() {
		::Legends.Effects.onCreate(this, ::Legends.Effect.LegendRswBleedingEffect);
		this.m.KilledString = "Bled to death";
		this.m.Icon = "skills/status_effect_01.png";
		this.m.IconMini = "status_effect_01_mini";
		this.m.Overlay = "status_effect_01";
		this.m.Type = this.Const.SkillType.StatusEffect | this.Const.SkillType.DamageOverTime;
		this.m.IsActive = false;
		this.m.IsStacking = true;
	}

	function getDescription() {
		return "This character is bleeding profusely from a recently received wound and will lose [color=%negative%]" + this.m.Damage + "[/color] hitpoints each turn for [color=%negative%]" + this.m.TurnsLeft + "[/color] more turn(s).";
	}

	function getAttacker() {
		if (!::Legends.Mod.ModSettings.getSetting("BleedKiller").getValue())
			return this.getContainer().getActor();

		if (::MSU.isNull(this.m.Actor))
			return this.getContainer().getActor();

		if (this.m.Actor.getID() != this.getContainer().getActor().getID()) {
			if (this.m.Actor.isAlive() && this.m.Actor.isPlacedOnMap()) {
				return this.m.Actor;
			}
		}

		return this.getContainer().getActor();
	}

	function applyDamage() {
		if (this.m.LastRoundApplied != this.Time.getRound()) {
			this.m.LastRoundApplied = this.Time.getRound();
			this.spawnIcon("status_effect_01", this.getContainer().getActor().getTile());
			local hitInfo = clone this.Const.Tactical.HitInfo;
			hitInfo.DamageRegular = this.m.Damage;
			hitInfo.DamageDirect = 1.0;
			hitInfo.BodyPart = this.Const.BodyPart.Body;
			hitInfo.BodyDamageMult = 1.0;
			hitInfo.FatalityChanceMult = 0.0;
			this.getContainer().getActor().onDamageReceived(this.getAttacker(), this, hitInfo);


			if (--this.m.TurnsLeft <= 0) {
				this.removeSelf();
			}
		}
	}

	function onAdded() {
		this.m.TurnsLeft = this.Math.max(1, this.m.TurnsLeft + this.getContainer().getActor().getCurrentProperties().NegativeStatusEffectDuration);

		if (this.getContainer().hasTrait(::Legends.Trait.Bleeder)) {
			++this.m.TurnsLeft;
		}
	}

	function onUpdate( _properties ) {
	}

	function onTurnEnd() {
		this.applyDamage();
	}

	function onWaitTurn() {
		this.applyDamage();
	}

	function onCombatFinished() {
		this.removeSelf();
	}
});
