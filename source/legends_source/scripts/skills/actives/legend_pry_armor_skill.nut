this.legend_pry_armor_skill <- this.inherit("scripts/skills/skill", {
	m = {
		IsPolearm = false,
	},
	function create()
	{
		::Legends.Actives.onCreate(this, ::Legends.Active.LegendPryArmor);
		this.m.Description = "Tear a weak spot in your opponent\'s armor to reveal a weakspot. Will apply Compromised Armor on every successful hit, which increases damage ignoring armor and armor damage by 20%.";
		this.m.KilledString = "Torn Apart";
		this.m.Icon = "skills/legend_active_pry_armor_warhammer.png";
		this.m.IconDisabled = "skills/legend_active_pry_armor_warhammer_bw.png";
		this.m.Overlay = "active_legend_pry_armor_warhammer";
		this.m.SoundOnUse = [
			"sounds/combat/crush_armor_01.wav",
			"sounds/combat/crush_armor_02.wav",
			"sounds/combat/crush_armor_03.wav"
		];
		this.m.SoundOnHit = [
			"sounds/combat/crush_armor_hit_01.wav",
			"sounds/combat/crush_armor_hit_02.wav",
			"sounds/combat/crush_armor_hit_03.wav"
		];
		this.m.Type = this.Const.SkillType.Active;
		this.m.Order = this.Const.SkillOrder.OffensiveTargeted;
		this.m.IsSerialized = false;
		this.m.IsActive = true;
		this.m.IsTargeted = true;
		this.m.IsStacking = false;
		this.m.IsAttack = true;
		this.m.IsIgnoredAsAOO = true;
		this.m.IsWeaponSkill = true;
		this.m.InjuriesOnBody = this.Const.Injury.BluntBody;
		this.m.InjuriesOnHead = this.Const.Injury.BluntHead;
		this.m.HitChanceBonus = 0;
		this.m.DirectDamageMult = 0.1;
		this.m.ActionPointCost = 4;
		this.m.FatigueCost = 25;
		this.m.MinRange = 1;
		this.m.MaxRange = 1;
		this.m.ChanceDecapitate = 0;
		this.m.ChanceDisembowel = 0;
		this.m.ChanceSmash = 50;
	}

	function setItem (_item) {
		if (this.m.IsPolearm) {
			this.m.Description = "Tear a weak spot in your opponent\'s armor to reveal a weakspot. Will apply Compromised Armor on every successful hit, which increases damage ignoring armor and armor damage by 20%. Can cover the distance of 2 tiles and can be used from behind the frontline, outside the range of most melee weapons.";
			this.m.FatigueCost = 35;
			this.m.ActionPointCost = 6;
			this.m.MaxRange = 2;
			this.m.Icon = "skills/legend_active_pry_armor_polehammer.png";
			this.m.IconDisabled = "skills/legend_active_pry_armor_polehammer_bw.png";
			this.m.Overlay = "active_legend_pry_armor_polehammer";
		}
		this.skill.setItem(_item);
	}

	function getTooltip()
	{
		local ret = this.getDefaultTooltip();
		if (this.m.MaxRange == 2)
		{
			ret.push({
				id = 7,
				type = "text",
				icon = "ui/icons/vision.png",
				text = "Has a range of [color=%positive%]2[/color] tiles"
			});
		}
		ret.extend([
			{
				id = 6,
				type = "text",
				icon = "ui/icons/hitchance.png",
				text = "Applies [color=%status%]Compromised Armor[/color] on successful hits"
			}
		]);
		return ret;
	}

	function onAfterUpdate( _properties )
	{
		if (this.m.IsPolearm && ::Legends.S.isCharacterWeaponSpecialized(_properties, this.getItem())) {
			this.m.ActionPointCost -= 1;
		}
		this.m.FatigueCostMult = ::Legends.S.isCharacterWeaponSpecialized(_properties, this.getItem()) ? this.Const.Combat.WeaponSpecFatigueMult : 1.0;
	}

	function onTargetHit ( _skill, _targetEntity, _bodyPart, _damageInflictedHitpoints, _damageInflictedArmor )
	{
		if (_skill != this)
			return;

		if (::Legends.S.skillEntityAliveCheck(this.getContainer().getActor(), _targetEntity))
			return;

		::Legends.Effects.grant(_targetEntity, ::Legends.Effect.LegendCompromisedArmor);
	}

	function onUse( _user, _targetTile )
	{
		this.spawnAttackEffect(_targetTile, this.Const.Tactical.AttackEffectThrust);
		return this.attackEntity(_user, _targetTile.getEntity());
	}

	function onAnySkillUsed( _skill, _targetEntity, _properties )
	{
		if (_skill == this)
		{
			_properties.DamageTotalMult *= 0.1;
			if (_targetEntity != null && !::Legends.S.isCharacterWeaponSpecialized(_properties, this.getItem()) && this.getContainer().getActor().getTile().getDistanceTo(_targetEntity.getTile()) == 1)
			{
				_properties.MeleeSkill -= 15;
				this.m.HitChanceBonus -= -15;
			}
		}
	}

});

