::mods_hookExactClass("skills/backgrounds/character_background", function(o)
{
	o.m.Names = [];
	o.m.Name <- "";
	o.m.BackgroundType <- this.Const.BackgroundType.None;
	o.m.AlignmentMin <- this.Const.LegendMod.Alignment.Dreaded;
	o.m.AlignmentMax <- this.Const.LegendMod.Alignment.Saintly;
	o.m.Modifiers <- {
		Ammo = this.Const.LegendMod.ResourceModifiers.Ammo[0],
		ArmorParts = this.Const.LegendMod.ResourceModifiers.ArmorParts[0],
		Meds = this.Const.LegendMod.ResourceModifiers.Meds[0],
		Stash = this.Const.LegendMod.ResourceModifiers.Stash[0],
		Healing = this.Const.LegendMod.ResourceModifiers.Healing[0],
		Injury = this.Const.LegendMod.ResourceModifiers.Injury[0],
		Repair = this.Const.LegendMod.ResourceModifiers.Repair[0],
		Salvage = this.Const.LegendMod.ResourceModifiers.Salvage[0],
		Crafting = this.Const.LegendMod.ResourceModifiers.Crafting[0],
		Barter = this.Const.LegendMod.ResourceModifiers.Barter[0],
		ToolConsumption = this.Const.LegendMod.ResourceModifiers.ToolConsumption[0],
		MedConsumption = this.Const.LegendMod.ResourceModifiers.MedConsumption[0],
		Hunting = this.Const.LegendMod.ResourceModifiers.Hunting[0],
		Fletching = this.Const.LegendMod.ResourceModifiers.Fletching[0],
		Scout = this.Const.LegendMod.ResourceModifiers.Scout[0],
		Gathering = this.Const.LegendMod.ResourceModifiers.Gather[0],
		Training = this.Const.LegendMod.ResourceModifiers.Training[0],
		Enchanting = 0.0,
		Terrain = [
			0.0, // ?
			0.0, //ocean
			0.0,//plains
			0.0, //swamp
			0.0, //hills
			0.0, //forest (pine?)
			0.0, //forest (snow?)
			0.0, //forest_leaves
			0.0, //autumn_forest
			0.0, //mountains
			0.0, // ?
			0.0, //farmland
			0.0, // snow
			0.0, // badlands
			0.0, //highlands
			0.0, //stepps
			0.0, //ocean
			0.0, //desert
			0.0 //oasis
		]
	};
	o.m.PerkTreeDynamicMins <- {
		Weapon = 8,
		Defense = 2,
		Traits = 7,
		Enemy = 1,
		EnemyChance = 0.01,
		Class = 1,
		ClassChance = 0.01,
		Profession = 1,
		ProfessionChance = 0.01,
		Magic = 1,
		MagicChance = 0
	};
	o.m.PerkTreeCustomMins <- { // overwritten if needed
		Weapon = 0,
		Defense = 0,
		Traits = 0,
		Enemy = 0,
		EnemyChance = 0.00,
		Class = 0,
		ClassChance = 0.00,
		Magic = 0,
		MagicChance = 0
	};
	o.m.PerkTreeDynamicBase <- { // this is a base perk tree so even if you don't add custom or dynamic perk tree it will default to this and build an average bro
		Weapon = [
			::Const.Perks.SwordTree,
			::Const.Perks.SpearTree,
			::Const.Perks.MaceTree
		],
		Defense = [
			::Const.Perks.MediumArmorTree
		],
		Traits = [
			::Const.Perks.FitTree,
			::Const.Perks.FastTree,
			::Const.Perks.AgileTree
		],
		Enemy = [],
		Class = [],
		Magic = []
	};
	o.m.PerkTreeDynamic <- null;
	o.m.CustomPerkTree <- null;
	o.m.PerkTreeMap <- null;
	o.m.PerkTree <- null;
	o.m.IsGuaranteed <- [];

	local create = o.create;
	o.create = function()
	{
		create();
		this.convertToBackgroundType();
	}

	o.convertToBackgroundType <- function ()
	{
		local function addToBackgroundType(_type, _constType){
			if (!_type) return
			this.m.BackgroundType = this.m.BackgroundType == this.Const.BackgroundType.None ? _constType : this.m.BackgroundType | _constType
		}
		addToBackgroundType(this.m.IsScenarioOnly, ::Const.BackgroundType.Scenario);
		addToBackgroundType(this.m.IsUntalented, ::Const.BackgroundType.Untalented);
		addToBackgroundType(this.m.IsOffendedByViolence, ::Const.BackgroundType.OffendedByViolence);
		addToBackgroundType(this.m.IsCombatBackground, ::Const.BackgroundType.Combat);
		addToBackgroundType(this.m.IsNoble, ::Const.BackgroundType.Noble);
		addToBackgroundType(this.m.IsLowborn, ::Const.BackgroundType.Lowborn);
	}

	o.isBackgroundType <- function ( _type )
	{
		return (this.m.BackgroundType & _type) != 0;
	}

	o.addBackgroundType <- function ( _type )
	{
		if (!this.isBackgroundType(_type))
		{
			this.m.BackgroundType = this.m.BackgroundType | _type;
		}
		// else
		// {
		// 	this.logError(_type + " is already contained in " + this.getID());
		// }
	}

	o.removeBackgroundType <- function ( _type )
	{
		if (this.isBackgroundType(_type))
		{
			this.m.BackgroundType -= _type;
		}
		else
		{
			this.logError(_type + " is not contained in " + this.getID());
		}
	}

	local isUntalented = o.isUntalented;
	o.isUntalented = function () {
		return isUntalented() || this.isBackgroundType(::Const.BackgroundType.Untalented);
	}

	local isOffendedByViolence = o.isOffendedByViolence;
	o.isOffendedByViolence = function () {
		return isOffendedByViolence() || this.isBackgroundType(::Const.BackgroundType.OffendedByViolence);
	}

	local isCombatBackground = o.isCombatBackground;
	o.isCombatBackground = function () {
		return isCombatBackground() || this.isBackgroundType(::Const.BackgroundType.Combat);
	}

	local isNoble = o.isNoble;
	o.isNoble = function () {
		return isNoble() || this.isBackgroundType(::Const.BackgroundType.Noble);
	}

	local isLowborn = o.isLowborn;
	o.isLowborn = function () {
		return isLowborn() || this.isBackgroundType(::Const.BackgroundType.Lowborn);
	}

	o.getModifiers <- function() {
		return this.m.Modifiers;
	}

	o.getIconColored <- function ()
	{
		if(this.isBackgroundType(this.Const.BackgroundType.ConvertedCultist)) {
			return "ui/backgrounds/background_34.png";
		}
		return this.m.Icon;
	}

	o.isHidden = function ()
	{
		return this.skill.isHidden() || this.isBackgroundType(this.Const.BackgroundType.Scenario);
	}

	o.getName = function ()
	{
		if(this.isBackgroundType(this.Const.BackgroundType.ConvertedCultist)) {
			return "Background: Cultist " + this.m.Name;
		}
		return "Background: " + this.m.Name;
	}

	o.getPerkTreeGroupDescription <- function ( _p, _prefix = "")
	{
		if( _p.len() == 0) {
			return ""
		}
		local i = 0;
		local text = _prefix + " " + _p[i].Descriptions[this.Math.rand(0, _p[i].Descriptions.len() - 1)];
		if (i == _p.len() - 1)
		{
			return text + ".\n";
		}

		i = 1;
		if (i == _p.len() - 1)
		{
			text = text + " and " + _p[i].Descriptions[this.Math.rand(0, _p[i].Descriptions.len() - 1)];
			return text + ".\n";
		}

		text = text + ", ";
		for (i; i < _p.len(); i++)
		{
			text = text + _p[i].Descriptions[this.Math.rand(0, _p[i].Descriptions.len() - 1)];
			if (i <  _p.len() - 2)
			{
				text = text + ", ";
			}
			else if (i <  _p.len() - 1)
			{
				text = text + " and "
			}
		}
		return text + ".\n";
	}

	o.getPerkBackgroundDescription <- function ( _tree )
	{
		local text = "";
		if (_tree == null) // donkeys don't have tree
			return text;
		text += this.getPerkTreeGroupDescription(_tree.Weapon,  "Has an aptitude for");
		text += this.getPerkTreeGroupDescription(_tree.Defense,  "Likes wearing");
		text += this.getPerkTreeGroupDescription(_tree.Enemy,  "Prefers fighting");
		text += this.getPerkTreeGroupDescription(_tree.Class,  "Is skilled in") ;
		text += this.getPerkTreeGroupDescription(_tree.Traits,  "");
		return text;
	}

	// Deprecated. New approach uses getBackgroundDescriptionTooltip
	o.getBackgroundDescription = function ( _desc )
	{
		local text = "";
		if (_desc)
		{
			text = text + this.m.BackgroundDescription + "\n";
			text = text + "\n" + this.getPerkBackgroundDescription(this.m.PerkTreeDynamic) + "\n";
		}

		local mtext = "";
		foreach (k, v in this.m.Modifiers)
		{
			if (k == "Terrain")
			{
				continue;
			}

			if (v == 0)
			{
				continue;
			}

			switch (k)
			{
				case "Ammo":
				case "Meds":
				case "Stash":
					mtext += k + " +" + v + "\n";
					break;
				case "ArmorParts":
					mtext += "Tools +" + v + "\n";
					break;
				default:
					v = v * 100;
					mtext += k + " +" + v + "%\n";
			}
		}
		if (mtext != "")
		{
			text += "\n" + mtext;
		}

		local terrains = this.m.Modifiers.Terrain;
		local val = 0.0;
		local ttext = "";
		val = terrains[2] * 100.0;
		if (val > 0) {
			ttext += "\nPlains +" + val +"%"
		}
		val = terrains[3] * 100.0;
		if (val > 0) {
			ttext += "\nSwamp +" + val +"%"
		}
		val = terrains[4] * 100.0;
		if (val > 0) {
			ttext += "\nHills +" + val +"%"
		}
		val = terrains[5] * 100.0;
		if (val > 0) {
			ttext += "\nForests +" + val +"%"
		}
		val = terrains[9] * 100.0;
		if (val > 0) {
			ttext += "\nMountains +" + val +"%"
		}
		val = terrains[11] * 100.0;
		if (val > 0) {
			ttext += "\nFarmland +" + val +"%"
		}
		val = terrains[12] * 100.0;
		if (val > 0) {
			ttext += "\nSnow +" + val +"%"
		}
		val = terrains[13] * 100.0;
		if (val > 0) {
			ttext += "\nBadlands +" + val +"%"
		}
		val = terrains[14] * 100.0;
		if (val > 0) {
			ttext += "\nHighlands +" + val +"%"
		}
		val = terrains[15] * 100.0;
		if (val > 0) {
			ttext += "\nStepps +" + val +"%"
		}
		val = terrains[17] * 100.0;
		if (val > 0) {
			ttext += "\nDeserts +" + val +"%"
		}
		val = terrains[18] * 100.0;
		if (val > 0) {
			ttext += "\nOases +" + val +"%"
		}

		if (ttext != "")
		{
		 	text += "\nTerrain Movement Modifiers:" + ttext;
		}
		return text;
	}

	o.getBackgroundDescriptionTooltip <- function ( _desc )
	{
		local tooltip = [];

		if (_desc)
		{
			tooltip.extend([
				{
					id = 2,
					type = "description",
					text = this.m.BackgroundDescription
				},
				{
					id = 3,
					type = "text",
					text = this.getPerkBackgroundDescription(this.m.PerkTreeDynamic)
				}
			]);
		}

		local capacities = [
			{
				key = "Stash",
				name = "Inventory Space",
				icon = "ui/icons/bag_icon.png"
			},
			{
				key = "ArmorParts",
				name = "Tools and Supplies",
				icon = "ui/icons/asset_supplies_icon.png"
			},
			{
				key = "Ammo",
				name = "Ammunition"
				icon = "ui/icons/asset_ammo_icon.png"
			},
			{
				key = "Meds",
				name = "Medical Supplies",
				icon = "ui/icons/asset_medicine_icon.png"
			}
		];

		local capacityTitle = true;
		foreach (c in capacities)
		{
			if (this.m.Modifiers[c.key] != 0)
			{
				if (capacityTitle)
				{
					tooltip.push(
						{
							id = 4,
							type = "hint",
							text = "[u]Company Capacity[/u]"
						}
					);
					capacityTitle = false;
				}

				tooltip.push(
					{
						id = 4,
						type = "hint",
						icon = c.icon,
						text = c.name + " " + ::Legends.S.colorize("" + ::Legends.S.getSign(this.m.Modifiers[c.key]) + this.Math.abs(this.m.Modifiers[c.key]), this.m.Modifiers[c.key])
					}
				);
			}
		}

		local skills = [
			{
				key = "Healing",
				name = "Healing",
				icon = "ui/icons/banner_heal_icon.png"
			},
			{
				key = "Injury",
				name = "Treating Injuries",
				icon = "ui/icons/banner_heal_icon.png"
			},
			{
				key = "MedConsumption",
				name = "Medical Supplies Efficiency"
				icon = "ui/icons/banner_heal_icon.png"
			},
			{
				key = "Repair",
				name = "Repairing",
				icon = "ui/icons/banner_repair_icon.png"
			},
			{
				key = "ToolConsumption",
				name = "Tools Efficiency",
				icon = "ui/icons/banner_repair_icon.png"
			},
			{
				key = "Salvage",
				name = "Salvaging",
				icon = "ui/icons/banner_scrap_icon.png"
			},
			{
				key = "Crafting",
				name = "Crafting"
				icon = "ui/icons/banner_craft_icon.png"
			},
			{
				key = "Scout",
				name = "Scouting",
				icon = "ui/icons/banner_scout_icon.png"
			},
			{
				key = "Training",
				name = "Training",
				icon = "ui/icons/banner_train_icon.png"
			},
			{
				key = "Fletching",
				name = "Fletching",
				icon = "ui/icons/banner_fletch_icon.png"
			},
			{
				key = "Gathering",
				name = "Gathering Supplies"
				icon = "ui/icons/banner_gather_icon.png"
			},
			{
				key = "Hunting",
				name = "Hunting",
				icon = "ui/icons/banner_hunt_icon.png"
			},
			{
				key = "Enchanting",
				name = "Enchanting",
				icon = "ui/icons/banner_enchant_icon.png"
			},
			{
				key = "Barter",
				name = "Bartering",
				icon = "ui/icons/banner_rest_icon.png"
			}
		];

		local skillsTitle = true;
		foreach (s in skills)
		{
			if (this.m.Modifiers[s.key] != 0)
			{
				if (skillsTitle)
				{
					tooltip.push(
						{
							id = 5,
							type = "hint",
							text = "[u]Company Skills[/u]"
						}
					);
					skillsTitle = false;
				}

				tooltip.push(
					{
						id = 5,
						type = "hint",
						icon = s.icon,
						text = s.name + " " + ::Legends.S.colorize("" + ((this.m.Modifiers[s.key] > 0) ? "+" : "-") + (this.m.Modifiers[s.key] * 100) + "%", this.m.Modifiers[s.key])
					}
				);
			}
		}

		local terrain = [
			{
				key = 2,
				name = "Plains"
			},
			{
				key = 3,
				name = "Swamps"
			},
			{
				key = 4,
				name = "Hills"
			},
			{
				key = 5,
				name = "Forests"
			},
			{
				key = 9,
				name = "Mountains"
			},
			{
				key = 11,
				name = "Farmland"
			},
			{
				key = 12,
				name = "Snow"
			},
			{
				key = 13,
				name = "Badlands"
			},
			{
				key = 14,
				name = "Highlands"
			},
			{
				key = 15,
				name = "Steppes"
			},
			{
				key = 17,
				name = "Deserts"
			},
			{
				key = 18,
				name = "Oases"
			}
		];
		local terrainTitle = true;
		foreach (t in terrain)
		{
			if (this.m.Modifiers.Terrain[t.key] != 0)
			{
				if (terrainTitle)
				{
					tooltip.push(
						{
							id = 6,
							type = "hint",
							text = "[u]Terrain Movement Modifiers[/u]"
						}
					);
					terrainTitle = false;
				}

				tooltip.push(
					{
						id = 6,
						type = "hint",
						icon = "ui/icons/tracking_disabled.png",
						text = t.name + " " + ::Legends.S.colorize("" + ((this.m.Modifiers.Terrain[t.key] > 0) ? "+" : "-") + (this.m.Modifiers.Terrain[t.key] * 100) + "%", this.m.Modifiers.Terrain[t.key])
					}
				);
			}
		}

		return tooltip;
	}

	o.getGenericTooltip <- function ()
	{
		local tooltip = [
			{
				id = 1,
				type = "title",
				text = this.getName()
			}
		];

		if (::Legends.Mod.ModSettings.getSetting("ShowCharacterBackgroundType").getValue())
		{
			tooltip.push({
				id = 2,
				type = "description",
				text = this.getBackgroundTypes() + "\n\n"
			});
		}

		tooltip.extend(this.getBackgroundDescriptionTooltip(true));

		return tooltip;
	}

	o.getTooltip <- function ()
	{
		local ret =  [
			{
				id = 1,
				type = "title",
				text = this.getName()
			},
			{
				id = 2,
				type = "description",
				text = this.getDescription()
			}
		];
		if (this.getContainer() != null && this.getContainer().getActor().getLevel() >= 12)
		{
			ret.push({
				id = 10,
				type = "text",
				icon = "ui/icons/special.png",
				text = "Gain a perk point every [color=%positive%]" + this.getContainer().getActor().getVeteranPerks() + "[/color] Levels"
			});
		}
		if (this.getContainer() != null) ret.extend(this.getAttributesTooltip());
		return ret;
	}

	o.getAttributesTooltip <- function()
	{
		if (this.getContainer().getActor().getLevel() >= 12) {
			return [];
		}

		local calculateAttribute = function(attribute, maximum = false)
		{
			local bro = this.getContainer().getActor();
			local attributeMin = this.Const.AttributesLevelUp[attribute].Min + this.Math.min(bro.m.Talents[attribute], 2);
			local attributeMax = this.Const.AttributesLevelUp[attribute].Max;
			if (bro.m.Talents[attribute] == 3) attributeMax += 1;
			local levelUps = this.Math.max(12 - bro.getLevel() + bro.getLevelUps(), 0);
			local attributeValue = maximum ? attributeMax * levelUps : attributeMin * levelUps;

			switch (attribute)
			{
				case this.Const.Attributes.Hitpoints:
					return attributeValue + bro.getBaseProperties().Hitpoints;
					break;
				case this.Const.Attributes.Bravery:
					return attributeValue + bro.getBaseProperties().Bravery;
					break;
				case this.Const.Attributes.Fatigue:
					return attributeValue + bro.getBaseProperties().Stamina;
					break;
				case this.Const.Attributes.Initiative:
					return attributeValue + bro.getBaseProperties().Initiative;
					break;
				case this.Const.Attributes.MeleeSkill:
					return attributeValue + bro.getBaseProperties().MeleeSkill;
					break;
				case this.Const.Attributes.RangedSkill:
					return attributeValue + bro.getBaseProperties().RangedSkill;
					break;
				case this.Const.Attributes.MeleeDefense:
					return attributeValue + bro.getBaseProperties().MeleeDefense;
					break;
				case this.Const.Attributes.RangedDefense:
					return attributeValue + bro.getBaseProperties().RangedDefense;
					break;
				default:
					return 0;
					break;
			}
		}

		local a = {
			Hitpoints = [
				calculateAttribute(this.Const.Attributes.Hitpoints),
				calculateAttribute(this.Const.Attributes.Hitpoints, true)
			],
			Bravery = [
				calculateAttribute(this.Const.Attributes.Bravery),
				calculateAttribute(this.Const.Attributes.Bravery, true)
			],
			Fatigue = [
				calculateAttribute(this.Const.Attributes.Fatigue),
				calculateAttribute(this.Const.Attributes.Fatigue, true)
			],
			Initiative = [
				calculateAttribute(this.Const.Attributes.Initiative),
				calculateAttribute(this.Const.Attributes.Initiative, true)
			],
			MeleeSkill = [
				calculateAttribute(this.Const.Attributes.MeleeSkill),
				calculateAttribute(this.Const.Attributes.MeleeSkill, true)
			],
			RangedSkill = [
				calculateAttribute(this.Const.Attributes.RangedSkill),
				calculateAttribute(this.Const.Attributes.RangedSkill, true)
			],
			MeleeDefense = [
				calculateAttribute(this.Const.Attributes.MeleeDefense),
				calculateAttribute(this.Const.Attributes.MeleeDefense, true)
			],
			RangedDefense = [
				calculateAttribute(this.Const.Attributes.RangedDefense),
				calculateAttribute(this.Const.Attributes.RangedDefense, true)
			]
		};

		local bufferHealth = "";
		local bufferFatigue = "";
		local bufferBravery = "";
		local bufferInitiative = "";

		if (a.Hitpoints[0] >= 100)
		{
			bufferFatigue += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Hitpoints[1] >= 100)
		{
			bufferFatigue += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Fatigue[0] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Fatigue[1] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Bravery[0] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferFatigue += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Bravery[1] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferFatigue += "&nbsp;&nbsp;";
			bufferInitiative += "&nbsp;&nbsp;";
		}
		if (a.Initiative[0] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferFatigue += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
		}
		if (a.Initiative[1] >= 100)
		{
			bufferHealth += "&nbsp;&nbsp;";
			bufferFatigue += "&nbsp;&nbsp;";
			bufferBravery += "&nbsp;&nbsp;";
		}

		local tooltip = [
			{
				id = 103,
				type = "hint",
				text = "Projection of this character\'s base attribute ranges calculated as if that attribute is improved on every level up from current level to 12."
			},
			{
				id = 104,
				type = "hint",
				text = "[img]gfx/ui/icons/health_va11.png[/img] " + a.Hitpoints[0] + " to " + a.Hitpoints[1] + bufferHealth + "&nbsp;&nbsp;&nbsp;[img]gfx/ui/icons/melee_skill_va11.png[/img] " + a.MeleeSkill[0] + " to " + a.MeleeSkill[1]
			},
			{
				id = 105,
				type = "hint",
				text = "[img]gfx/ui/icons/fatigue_va11.png[/img] " + a.Fatigue[0] + " to " + a.Fatigue[1] + bufferFatigue + "&nbsp;&nbsp;&nbsp;[img]gfx/ui/icons/ranged_skill_va11.png[/img] " + a.RangedSkill[0] + " to " + a.RangedSkill[1]
			},
			{
				id = 106,
				type = "hint",
				text = "[img]gfx/ui/icons/bravery_va11.png[/img] " + a.Bravery[0] + " to " + a.Bravery[1] + bufferBravery + "&nbsp;&nbsp;&nbsp;[img]gfx/ui/icons/melee_defense_va11.png[/img] " + a.MeleeDefense[0] + " to " + a.MeleeDefense[1]
			},
			{
				id = 107,
				type = "hint",
				text = "[img]gfx/ui/icons/initiative_va11.png[/img] " + a.Initiative[0] + " to " + a.Initiative[1] + bufferInitiative + "&nbsp;&nbsp;&nbsp;[img]gfx/ui/icons/ranged_defense_va11.png[/img] " + a.RangedDefense[0] + " to " + a.RangedDefense[1]
			}
		];

		return tooltip;
	}

	o.getPerkTreeDescription <- function ()
	{
		local text = "";
		foreach (i, group in this.m.PerkTree)
		{
			text += "\nTier " + (i + 1) + ": ";
			foreach (p in group)
			{
				text += p.Name + ", ";
			}
		}
		return text;
	}

	o.getPerkTree <- function ()
	{
		if (this.m.PerkTree != null)
		{
			return this.m.PerkTree;
		}

		local pT = ::Const.Perks.PerksTreeTemplate;
		if (pT == null)
		{
			return [];
		}
		return pT.Tree;
	}

	// Input can be a Perk Id or a PerkDef
	o.getPerk <- function ( _perk )
	{
		local id;
		local perkDef;
		if (typeof _perk == "string")
		{
			id = _perk;
			local basePerkDefObject = ::Const.Perks.findById(_perk);
			perkDef = ::Legends.Perk[basePerkDefObject.Const];
		}
		else
		{
			id = ::Const.Perks.PerkDefObjects[_perk].ID;
			perkDef = _perk;
		}

		if (!(id in this.m.PerkTreeMap))
		{
			return null;
		}

		return this.m.PerkTreeMap[id];
	}

    o.addPerk <- function ( _perk, _preferredRow = 0, _isRefundable = true )
    {
        local perkDefObject = clone ::Const.Perks.PerkDefObjects[_perk];

        // Don't add duplicates
        if (this.m.PerkTreeMap == null || perkDefObject.ID in this.m.PerkTreeMap)
        {
            return false;
        }

        // Attempt to find a valid row
        local finalRow = _preferredRow;
        local foundRow = false;

        for (local i = 0; i <= 6; i++)
        {
            local tryRow = (_preferredRow + i) % 7;

            // Ensure row exists
            while (this.getPerkTree().len() <= tryRow)
            {
                this.getPerkTree().push([]);
            }

            if (this.getPerkTree()[tryRow].len() < 13)
            {
                finalRow = tryRow;
                foundRow = true;
                break;
            }
        }

        if (!foundRow)
        {
            // All rows are full, fallback to preferredRow
            finalRow = _preferredRow;
        }

        perkDefObject.Row <- finalRow;
        perkDefObject.Unlocks <- finalRow;
        perkDefObject.IsRefundable <- _isRefundable;

        // Extend perk tree if not enough rows exist
        while (this.getPerkTree().len() <= finalRow)
        {
            this.getPerkTree().push([]);
        }
        while (this.m.CustomPerkTree.len() <= finalRow)
        {
            this.m.CustomPerkTree.push([]);
        }

        this.getPerkTree()[finalRow].push(perkDefObject);
        this.m.CustomPerkTree[finalRow].push(_perk);
        this.m.PerkTreeMap[perkDefObject.ID] <- perkDefObject;

        return true;
    }

	o.addPerkGroup <- function (_Tree) {
		foreach(index, arrAdd in _Tree)
		{
			foreach (perkAdd in arrAdd)
			{
				this.addPerk(perkAdd, index);
			}
		}
	}

	o.removePerk <- function ( _perk )
	{
		local perkDefObject = ::Const.Perks.PerkDefObjects[_perk];
		if (!(perkDefObject.ID in this.m.PerkTreeMap))
		{
			return false;
		}

		local row = this.m.PerkTreeMap[perkDefObject.ID].Row;

		local perkTree = this.getPerkTree();
		foreach (i, perk in perkTree[row])
		{
			if (perk.ID == perkDefObject.ID)
			{
				perkTree[row].remove(i);
				break;
			}
		}

		foreach (i, perk in this.m.CustomPerkTree[row])
		{
			if (perk == _perk)
			{
				this.m.CustomPerkTree[row].remove(i);
				break;
			}
		}

		delete this.m.PerkTreeMap[perkDefObject.ID];

		return true;
	}

	o.hasPerkGroup <- function ( _group )
	{
		foreach (row in _group.Tree)
		{
			foreach (perk in row)
			{
				if (!this.hasPerk(perk)) return false;
			}
		}

		return true;
	}

	o.removePerkGroup <- function ( _group )
	{
		foreach (i, row in _group.Tree)
		{
			foreach (perk in row)
			{
				this.removePerk(perk);
			}
		}
	}

	o.hasPerk <- function ( _perk )
	{
		return ::Const.Perks.PerkDefObjects[_perk].ID in this.m.PerkTreeMap;
	}

	/**
	 * Gets information on how many complete perk groups the character has,
	 * as well as any additional perks that are not part of a complete set
	 *
	 * @return A table containing the following:
	 * 	- CompleteGroupsIDs: Table whose keys are perk group categories; values are arrays containing IDs of complete perk groups
	 * 	- RemainingPerkDefs: Array of perkDefs (numbers representing indeces in ::Const.Perks.PerkDefObjects) that do not belong to any complete perk group
	 */
	o.getPerkGroups <- function ()
	{
		local tmp = {};
		local nonStrayPerks = {};
		local possibleStrayPerks = [];
		local ret = {
			CompleteGroupsIDs = ::Legends.Perks.buildPerkGroupCategoriesTableOfArrays(),
			RemainingPerkDefs = [] // Array of perkDefs that do not belong to any complete perk group
		}

		foreach (perk in this.m.PerkTreeMap)
		{
			// 1. Find out which perk groups are possible
			// 2. Check if each perk group is fully represented
			// 3. List out all fully represented perk groups, and the remaining number of ungrouped perks
			foreach (entry in perk.PerkGroups)
			{
				if (!(entry.ID in tmp))
				{
					tmp[entry.ID] <- {
						Category = entry.Category,
						PerkDefs = {}
					}
				}

				tmp[entry.ID].PerkDefs[Legends.Perk[perk.Const]] <- true;
			}
		}

		foreach (id, entry in tmp)
		{
			if (::Legends.Perks.isPerkGroupFullyRepresented(id, entry.PerkDefs))
			{
				ret.CompleteGroupsIDs[entry.Category].push(id);
				foreach (key, v in entry.PerkDefs)
				{
					if (!(key in nonStrayPerks))
					{
						nonStrayPerks[key] <- true;
					}
				}
			}
			else
			{
				foreach (key, v in entry.PerkDefs)
				{
					possibleStrayPerks.push(key);
				}
			}
		}

		foreach (perkDef in possibleStrayPerks)
		{
			if (!(perkDef in nonStrayPerks))
			{
				ret.RemainingPerkDefs.push(perkDef);
			}
		}

		return ret;
	}

	/**
	 * Update tooltip data to add a list of all perk groups this character has, organised by categories
	 *
	 * @param arr An array of tables to hold the tooltip data, as seen in tooltip_events.nut
	 */
	o.extendKnownPerksTooltip <- function(arr)
	{
		local data = this.getPerkGroups();
		local last = arr[arr.len() - 1];
		local iter = "id" in last ? last.id : 3;
		foreach (category in ::Legends.Perks.PerkGroupCategoriesOrder)
		{
			iter++;
			if (data.CompleteGroupsIDs[category].len() > 0)
			{
				arr.push({
					id = iter,
					type = "text",
					text = "\n[u]" + category + "[/u]"
				});
			}

			local counter = 0;

			foreach (index, group in data.CompleteGroupsIDs[category])
			{
				counter++;
				arr.push({
					id = iter,
					type = "textDualColumn",
					listCount = counter,
					listLength = data.CompleteGroupsIDs[category].len(),
					icon = "Icon" in ::Const.Perks[group] ? ::Const.Perks[group].Icon : "ui/perks/legend_vala_days.png",
					text = ::Const.Perks[group].Name
				});
			}
		}
	}

	o.buildDescription = function( _isFinal = false )
	{
		if (this.isBackgroundType(this.Const.BackgroundType.Scenario))
		{
			return;
		}

		local villages = this.World.EntityManager.getSettlements();
		local citystates = [];
		local northern = [];

		for( local i = 0; i < villages.len(); i = ++i )
		{
			if (this.isKindOf(villages[i], "city_state"))
			{
				citystates.push(villages[i]);
			}
			else
			{
				northern.push(villages[i]);
			}
		}

		local brother = this.World.getPlayerRoster().getAll();
		brother = brother.len() != 0 ? brother[this.Math.rand(0, brother.len() - 1)].getName() : "";
		local vars = [
			[
				"townname",
				this.World.State.getCurrentTown() != null ? this.World.State.getCurrentTown().getNameOnly() : villages[this.Math.rand(0, villages.len() - 1)].getNameOnly()
			],
			[
				"randomtown",
				northern[this.Math.rand(0, northern.len() - 1)].getNameOnly()
			],
			[
				"randomcity",
				northern[0].getNameOnly()
			],
			[
				"randomcitystate",
				citystates.len() != 0 ? citystates[this.Math.rand(0, citystates.len() - 1)].getNameOnly() : ""
			],
			[
				"randomname",
				this.Const.Strings.CharacterNames[this.Math.rand(0, this.Const.Strings.CharacterNames.len() - 1)]
			],
			[
				"randomnoble",
				this.Const.Strings.KnightNames[this.Math.rand(0, this.Const.Strings.KnightNames.len() - 1)]
			],
			[
				"randomvizier",
				this.Const.Strings.SouthernNames[this.Math.rand(0, this.Const.Strings.SouthernNames.len() - 1)] + " " + this.Const.Strings.VizierTitles[this.Math.rand(0, this.Const.Strings.VizierTitles.len() - 1)]
			],
			[
				"randomnamefemale",
				this.Const.Strings.CharacterNamesFemale[this.Math.rand(0, this.Const.Strings.CharacterNamesFemale.len() - 1)]
			],
			[
				"companyname",
				this.World.Assets.getName()
			],
			[
				"randombrother",
				brother
			]
		];
		this.onPrepareVariables(vars);

		if (!_isFinal)
		{
			this.m.RawDescription = this.buildTextFromTemplate(this.onBuildDescription(), vars);
		}

		vars.push([
			"name",
			this.getContainer().getActor().getNameOnly()
		]);
		vars.push([
			"fullname",
			this.getContainer().getActor().getName()
		]);
		vars.push([
			"title",
			this.getContainer().getActor().getTitle()
		]);

		::Const.LegendMod.extendVarsWithPronouns(vars, this.getContainer().getActor());

		this.m.Description = this.buildTextFromTemplate(this.m.RawDescription, vars);
	}

	o.buildAttributes = function (_tag = null, _attrs = null)
	{
		// helper function to sum all keys in the table, that ensures [min, max], regardless the input
		local sum = function (_a, _b) {
			local ret = {};
			foreach(k, v in _a) {
				local aMin = ::Math.min(v[0], v[1]);
				local aMax = ::Math.max(v[0], v[1]);
				if (k in _b) {
					local bv = _b[k];
					ret[k] <- [
						aMin + ::Math.min(bv[0], bv[1]),
						aMax + ::Math.max(bv[0], bv[1])
					];
				} else { // key doesn't exist in _b, so just use what's in _a
					ret[k] <- [aMin, aMax];
				}
			}
			return ret;
		}

		local a = clone ::Legends.Backgrounds.BaseAttr.resolve(_tag);
		// Modify the stats if being female carries a gameplay effect
		if (::Legends.Mod.ModSettings.getSetting("GenderEquality").getValue() == "Enabled") {
			if (this.getContainer().getActor().getGender() == 1) {
				a = sum(a, ::Legends.Backgrounds.BaseAttr.Female);
			}
		}

		a = sum(a, this.onChangeAttributes());
		if (_attrs != null)
			a = sum(a, _attrs);

		local b = this.getContainer().getActor().getBaseProperties();
		b.ActionPoints = 9;
		foreach(k, v in a) { // set avg of 2 rolls to `b`
			b[k] = ::Math.round((::Math.rand(v[0], v[1]) + ::Math.rand(v[0], v[1])) / 2);
		}

		this.getContainer().getActor().m.CurrentProperties = clone b;
		this.getContainer().getActor().setHitpoints(b.Hitpoints);

		local calc = function(_a, _b, _field) {
			local a = _a[_field];
			local b = _b[_field];
			if (a[1] == a[0])
				return 50;
			local weight = ::Math.floor(::Math.abs(b - a[0]) * 100.0 / ::Math.abs(a[1] - a[0]));
			if (weight == 0)
				return 50;
			return weight;
		}

		local weighted = [
			calc(a, b, "Hitpoints"),
			calc(a, b, "Bravery"),
			calc(a, b, "Stamina"),
			calc(a, b, "MeleeSkill"),
			calc(a, b, "RangedSkill"),
			calc(a, b, "MeleeDefense"),
			calc(a, b, "RangedDefense"),
			calc(a, b, "Initiative")
		];
		return weighted;
	}

	o.rebuildPerkTree <- function ( _tree )
	{
		this.m.CustomPerkTree = _tree;
		this.m.CustomPerkTree = ::Const.Perks.MergeDynamicPerkTree(_tree, this.m.PerkTreeDynamic);
		local pT = ::Const.Perks.BuildCustomPerkTree(this.m.CustomPerkTree);
		this.m.PerkTree = pT.Tree;
		this.m.PerkTreeMap = pT.Map;
	}

	o.getPerkTreeDynamicMins <- function ()
	{
		local mins = this.m.PerkTreeDynamicMins;
		if (this.isBackgroundType(this.Const.BackgroundType.Educated))
			mins.ProfessionChance += 0.09;
		if (this.isBackgroundType(this.Const.BackgroundType.Lowborn))
			mins.ClassChance += 0.09;
		return mins;
	}

	o.buildPerkTree <- function ()
	{
		local a = {
			Hitpoints = [
				0,
				0
			],
			Bravery = [
				0,
				0
			],
			Stamina = [
				0,
				0
			],
			MeleeSkill = [
				0,
				0
			],
			RangedSkill = [
				0,
				0
			],
			MeleeDefense = [
				0,
				0
			],
			RangedDefense = [
				0,
				0
			],
			Initiative = [
				0,
				0
			]
		};

		if (this.m.PerkTree != null)
		{
			return a;
		}

		if (this.m.CustomPerkTree == null)
		{
			local tree = this.m.PerkTreeDynamic == null ? this.m.PerkTreeDynamicBase : this.m.PerkTreeDynamic;
			local mins = this.getPerkTreeDynamicMins();

			local result  = ::Const.Perks.GetDynamicPerkTree(mins, tree, false);
			this.m.CustomPerkTree = result.Tree;
			a = result.Attributes;
		}

		local pT = ::Const.Perks.BuildCustomPerkTree(this.m.CustomPerkTree);
		this.m.PerkTree = pT.Tree;
		this.m.PerkTreeMap = pT.Map;

		if (this.m.PerkTreeDynamic != null)
		{
			this.rebuildPerkTree(this.m.CustomPerkTree)
		}

		//When deserializing, the scenario isn't set yet, so it will be null - in this case, the sceario should
		//already have added its perks so we should be ok. This will fail though loading an old save
		//and we've added new perks to a scenario...

		// THE COMMMENT ABOVE IS PROBABLY WRONG. Scenario doesn't seem to be null here on deserialize. But some weird
		// shenanigans are still happening, so I will test some more. -- Midas
		local origin = this.World.Assets.getOrigin();
		if (origin != null && this.getContainer() != null && !this.getContainer().getActor().getFlags().get("ScenarioPerkSet"))
		{
			this.getContainer().getActor().getFlags().set("ScenarioPerkSet", true);
			origin.onBuildPerkTree(this);
		}

		return a;
	}

	o.onUpdate = function ( _properties )
	{
		if (this.m.DailyCost == 0 || this.getContainer().hasTrait(::Legends.Trait.Player))
		{
			_properties.DailyWage = 0;
		}
		else
		{
			local level = this.getContainer().getActor().getLevel();
			local wage = this.Math.round(this.m.DailyCost * this.m.DailyCostMult);
			_properties.DailyWage += wage * this.Math.pow(1.1, this.Math.min(10, level - 1));

			if (level > 12)
			{
				local previous = wage * this.Math.pow(1.1, 10);
				_properties.DailyWage += previous * this.Math.pow(1.03, level - 1 - 10) - previous;
			}
		}

		if (("State" in this.World) && this.World.State != null && this.World.Assets.getOrigin() != null && this.World.Assets.getOrigin().getID() == "scenario.manhunters" && this.getID() != "background.slave")
		{
			_properties.XPGainMult *= 0.9;
		}
	}

	o.setAppearance = function(_tag = null)
	{
		if (this.m.HairColors == null)
		{
			return;
		}

		if (_tag == "zombie")
		{
			local actor = this.getContainer().getActor();
			local hairColor = this.Const.HairColors.Zombie[this.Math.rand(0, this.Const.HairColors.Zombie.len() - 1)];

			local body = actor.getSprite("body");
			local b = this.m.Bodies[this.Math.rand(0, this.m.Bodies.len() - 1)];
			body.setBrush(b);
			body.Saturation = 0.5;
			body.varySaturation(0.2);
			body.Color = this.createColor("#c1ddaa");
			body.varyColor(0.05, 0.05, 0.05);
			actor.getSprite("injury_body").setBrush("zombify_body_01");

			local head = actor.getSprite("head");
			head.setBrush(this.m.Faces[this.Math.rand(0, this.m.Faces.len() - 1)]);
			head.Saturation = body.Saturation;
			head.Color = body.Color;

			local hair = actor.getSprite("hair");
			hair.setBrush("hair_" + hairColor + "_" + this.Const.Hair.Zombie[this.Math.rand(0, this.Const.Hair.Zombie.len() - 1)]);
			hair.varyColor(0.02, 0.02, 0.02);

			if (this.m.Beards != null && this.Math.rand(1, 100) <= this.m.BeardChance)
			{
				local beard = actor.getSprite("beard");
				beard.setBrush("beard_" + hairColor + "_" + this.Const.Beards.Zombie[this.Math.rand(0, this.Const.Beards.Zombie.len() - 1)]);
				beard.Color = hair.Color;

				if (this.doesBrushExist(beard.getBrush().Name + "_top"))
				{
					local sprite = actor.getSprite("beard_top");
					sprite.setBrush(beard.getBrush().Name + "_top");
					sprite.Color = actor.getSprite("hair").Color;
				}
			}
		}
		else if (_tag == "skeleton")
		{
			local actor = this.getContainer().getActor();
			local hairColor = this.Const.HairColors.Zombie[this.Math.rand(0, this.Const.HairColors.Zombie.len() - 1)];

			local body = actor.getSprite("body");
			body.setBrush("bust_skeleton_body_0" + this.Math.rand(1, 2));
			body.Saturation = 0.8;
			body.varySaturation(0.2);
			body.varyColor(0.025, 0.025, 0.025);
			actor.getSprite("injury_body").setBrush("bust_skeleton_body_injured");

			local head = actor.getSprite("head");
			head.setBrush("bust_skeleton_head");
			head.Color = body.Color;
			head.Saturation = body.Saturation;

			local hair = actor.getSprite("hair");
			hair.setBrush("hair_" + hairColor + "_" + this.Const.Hair.ZombieOnly[this.Math.rand(0, this.Const.Hair.ZombieOnly.len() - 1)]);
			hair.varyColor(0.02, 0.02, 0.02);

			if (this.m.Beards != null && this.Math.rand(1, 100) <= this.m.BeardChance)
			{
				local beard = actor.getSprite("beard");
				beard.setBrush("beard_" + hairColor + "_" + this.Const.Beards.ZombieOnly[this.Math.rand(0, this.Const.Beards.ZombieOnly.len() - 1)]);
				beard.Color = hair.Color;

				if (this.doesBrushExist(beard.getBrush().Name + "_top"))
				{
					local sprite = actor.getSprite("beard_top");
					sprite.setBrush(beard.getBrush().Name + "_top");
					sprite.Color = actor.getSprite("hair").Color;
				}
			}
		}
		else
		{
			local actor = this.getContainer().getActor();
			local hairColor = this.m.HairColors[this.Math.rand(0, this.m.HairColors.len() - 1)];

			if (this.m.Faces != null)
			{
				local sprite = actor.getSprite("head");
				sprite.setBrush(this.m.Faces[this.Math.rand(0, this.m.Faces.len() - 1)]);
				sprite.Color = this.createColor("#fbffff");
				sprite.varyColor(0.05, 0.05, 0.05);
				sprite.varySaturation(0.1);
				local body = actor.getSprite("body");
				body.Color = sprite.Color;
				body.Saturation = sprite.Saturation;
			}

			if (this.m.Hairs != null && this.Math.rand(0, this.m.Hairs.len()) != this.m.Hairs.len())
			{
				local sprite = actor.getSprite("hair");
				sprite.setBrush("hair_" + hairColor + "_" + this.m.Hairs[this.Math.rand(0, this.m.Hairs.len() - 1)]);

				if (hairColor != "grey")
				{
					sprite.varyColor(0.1, 0.1, 0.1);
				}
				else
				{
					sprite.varyBrightness(0.1);
				}
			}

			if (this.m.Beards != null && this.Math.rand(1, 100) <= this.m.BeardChance)
			{
				local beard = actor.getSprite("beard");
				beard.setBrush("beard_" + hairColor + "_" + this.m.Beards[this.Math.rand(0, this.m.Beards.len() - 1)]);
				beard.Color = actor.getSprite("hair").Color;

				if (this.doesBrushExist(beard.getBrush().Name + "_top"))
				{
					local sprite = actor.getSprite("beard_top");
					sprite.setBrush(beard.getBrush().Name + "_top");
					sprite.Color = actor.getSprite("hair").Color;
				}
			}

			if (this.m.Bodies != null)
			{
				local body = this.m.Bodies[this.Math.rand(0, this.m.Bodies.len() - 1)];
				actor.getSprite("body").setBrush(body);
				actor.getSprite("injury_body").setBrush(body + "_injured");
			}

			if (this.m.Ethnicity == 1 && hairColor != "grey")
			{
				local hair = actor.getSprite("hair");
				hair.Saturation = 0.8;
				hair.setBrightness(0.4);
				local beard = actor.getSprite("beard");
				beard.Color = hair.Color;
				beard.Saturation = hair.Saturation;
				local beard_top = actor.getSprite("beard_top");
				beard_top.Color = hair.Color;
				beard_top.Saturation = hair.Saturation;
			}
		}

		this.onSetAppearance();
	}

	o.calculateAdditionalRecruitmentLevels <- function ()
	{
		if (::Legends.Mod.ModSettings.getSetting("RecruitScaling").getValue())
		{
			local roster = this.World.getPlayerRoster().getAll();
			local levels = 0;
			local count = 0;

			foreach( i, bro in roster )
			{
				local brolevel = bro.getLevel();
				levels = levels + brolevel;
				count = count + 1;
			}

			local avgLevel = this.Math.floor(levels / count);
			local busRep = this.World.Assets.getBusinessReputation();
			local repPoints = this.Math.floor(busRep / 1000);
			local repLevelAvg = this.Math.floor((avgLevel + repPoints) / 4);
			local broLevel = this.Math.rand(1, repLevelAvg);
			return broLevel - 1;
		}
		else
		{
			return 0;
		}
	}

	o.onAdded = function()
	{
		if (this.m.DailyCost > 0)
		{
			this.m.DailyCost += 1;
		}

		local actor = this.getContainer().getActor();
		actor.m.Background = this;
		actor.m.Ethnicity = this.m.Ethnicity;

		if (!this.m.IsNew)
		{
			return;
		}

		if (("State" in this.Tactical) && this.Tactical.State != null && this.Tactical.State.isScenarioMode())
		{
			return;
		}

		this.m.IsNew = false;

		if (this.m.LastNames.len() == 0 && this.m.Ethnicity == 1)
		{
			this.m.LastNames = this.Const.Strings.SouthernNamesLast;
		}

		if (actor.getTitle() == "" && this.m.LastNames.len() != 0 && this.Math.rand(0, 1) == 1)
		{
			actor.setTitle(this.m.LastNames[this.Math.rand(0, this.m.LastNames.len() - 1)]);
		}

		if (actor.getTitle() == "" && this.m.Titles.len() != 0 && this.Math.rand(0, 3) == 3)
		{
			actor.setTitle(this.m.Titles[this.Math.rand(0, this.m.Titles.len() - 1)]);
		}

		if (actor.getNameOnly() == "")
		{
			local names = this.m.Names;

			if (names == null || this.m.Names.len() == 0)
			{
				names = this.Const.Strings.CharacterNames;
				if (this.m.Ethnicity == 1)
				{
					names = this.Const.Strings.SouthernNames;
				}
				else if (this.m.Ethnicity == 2)
				{
					names = this.Const.Strings.BarbarianNames
				}

				if (this.isBackgroundType(this.Const.BackgroundType.Female))
				{
					names = this.Const.Strings.CharacterNamesFemale;
					if (this.m.Ethnicity == 1)
					{
						names = this.Const.Strings.SouthernFemaleNames;
					}
					else if (this.m.Ethnicity == 2)
					{
						names = this.Const.Strings.CharacterNamesFemaleNorse;
					}
				}
			}

			actor.setName(names[this.Math.rand(0, names.len() - 1)]);
		}

		this.m.Level += actor.m.Background.calculateAdditionalRecruitmentLevels();

		if (this.m.Level != 1)
		{
			if (this.m.Level <= 12)
			{
				actor.m.PerkPoints = this.m.Level - 1;
			}
			else
			{
				local vetPerk = this.getContainer().getActor().getVeteranPerks();
				if (vetPerk == 0)
				{
					actor.m.PerkPoints = 11;
				}
				else
				{
					actor.m.PerkPoints = 11 + this.Math.floor((this.m.Level - 12) / this.getContainer().getActor().getVeteranPerks());
				}
			}
			actor.m.PerkPoints = this.m.Level - 1;
			actor.m.LevelUps = this.m.Level - 1;
			actor.m.Level = this.m.Level;
			actor.m.XP = this.Const.LevelXP[this.m.Level - 1];
		}

	}
	o.onCombatStarted <- function()
	{
		local actor = this.getContainer().getActor();

		if (this.isBackgroundType(this.Const.BackgroundType.Female))
		{
			actor.m.Sound[this.Const.Sound.ActorEvent.NoDamageReceived] = [
				"sounds/humans/legends/woman_light_01.wav",
				"sounds/humans/legends/woman_light_02.wav",
				"sounds/humans/legends/woman_light_03.wav",
				"sounds/humans/legends/woman_light_04.wav",
				"sounds/humans/legends/woman_light_05.wav"
			];
			actor.m.Sound[this.Const.Sound.ActorEvent.DamageReceived] = [
				"sounds/humans/legends/woman_injury_01.wav",
				"sounds/humans/legends/woman_injury_02.wav",
				"sounds/humans/legends/woman_injury_03.wav"
			];
			actor.m.Sound[this.Const.Sound.ActorEvent.Death] = [
				"sounds/humans/legends/woman_death_01.wav",
				"sounds/humans/legends/woman_death_02.wav",
				"sounds/humans/legends/woman_death_03.wav"
			];
			actor.m.Sound[this.Const.Sound.ActorEvent.Fatigue] = [
				"sounds/humans/legends/woman_fatigue_01.wav",
				"sounds/humans/legends/woman_fatigue_02.wav",
				"sounds/humans/legends/woman_fatigue_03.wav",
				"sounds/humans/legends/woman_fatigue_04.wav",
				"sounds/humans/legends/woman_fatigue_05.wav",
				"sounds/humans/legends/woman_fatigue_06.wav",
				"sounds/humans/legends/woman_fatigue_07.wav"
			];
			actor.m.Sound[this.Const.Sound.ActorEvent.Flee] = [
				"sounds/humans/legends/woman_flee_01.wav",
				"sounds/humans/legends/woman_flee_02.wav",
				"sounds/humans/legends/woman_flee_03.wav",
				"sounds/humans/legends/woman_flee_04.wav",
				"sounds/humans/legends/woman_flee_05.wav",
				"sounds/humans/legends/woman_flee_06.wav"
			];
			actor.m.SoundPitch = this.Math.rand(105, 115) * 0.01;
		}
	}
	o.getBackgroundTypes <- function()
	{

		local ret = "";

		foreach (type, name in ::Const.BackgroundTypeName)
		{
		    if (this.isBackgroundType(::Const.BackgroundType[type]))
		    {
		        ret += name + ", ";
		    }
		}

		if (ret.len() == 0) return "";

		return "[color=%negative%]Background Type: " + ret.slice(0, ret.len() - 2) + "[/color]";
	}

	//0 = Male, 1 = Female, -1 = Either
	o.setGender <- function (_gender)
	{
	}

	o.Convert <- function()
	{
		this.addBackgroundType(this.Const.BackgroundType.ConvertedCultist);
		this.addPerkGroup(this.Const.Perks.NinetailsClassTree.Tree);
		this.getContainer().getActor().getFlags().add("cultist");
	}

	o.onSerialize = function ( _out )
	{
		this.skill.onSerialize(_out);
		_out.writeString(this.m.Description);
		_out.writeString(this.m.RawDescription);
		_out.writeU8(this.m.Level);
		_out.writeBool(this.m.IsNew);
		_out.writeF32(this.m.DailyCostMult);
		_out.writeBool(this.isBackgroundType(this.Const.BackgroundType.Female));
		_out.writeBool(this.isBackgroundType(this.Const.BackgroundType.ConvertedCultist));
		if (this.m.CustomPerkTree == null)
		{
			_out.writeU8(0);
		}
		else
		{
			_out.writeU8(this.m.CustomPerkTree.len());
			for( local i = 0; i < this.m.CustomPerkTree.len(); i = ++i )
			{
				_out.writeU8(this.m.CustomPerkTree[i].len());
				for( local j = 0; j < this.m.CustomPerkTree[i].len(); j = ++j )
				{
					_out.writeU16(this.m.CustomPerkTree[i][j]);
				}
			}
		}

		local nonRefundablePerks = [];
		foreach (row in this.m.PerkTree)
		{
			foreach (perk in row)
			{
				if (!perk.IsRefundable)
				{
					nonRefundablePerks.push(::Legends.Perk[perk.Const]);
				}
			}
		}

		_out.writeU8(nonRefundablePerks.len());
		foreach (perk in nonRefundablePerks)
		{
			_out.writeU16(perk);
		}
	}

	o.onDeserialize = function ( _in )
	{
		this.skill.onDeserialize(_in);
		this.m.Description = _in.readString();
		this.m.RawDescription = _in.readString();
		this.m.Level = _in.readU8();
		this.m.IsNew = _in.readBool();
		this.m.DailyCostMult = _in.readF32();

		if(_in.readBool())
		{
			this.addBackgroundType(this.Const.BackgroundType.Female);
			this.setGender(1)
		}
		else
		{
			this.setGender(0)
		}

		if (_in.readBool())
		{
			this.addBackgroundType(this.Const.BackgroundType.ConvertedCultist);
		}

		this.m.CustomPerkTree = [];
		local numRows = _in.readU8();
		for( local i = 0; i < numRows; i = ++i )
		{
			local numPerks = _in.readU8();
			local perks = [];
			for( local j = 0; j < numPerks; j = ++j )
			{
				perks.push(_in.readU16())
			}
			this.m.CustomPerkTree.push(perks);
		}

		if (this.m.CustomPerkTree != null)
		{
			this.buildPerkTree();
		}

		local num = _in.readU8();
		for (local i = 0; i < num; i++)
		{
			this.getPerk(_in.readU16()).IsRefundable <- false;
		}
	}

	// little hack to remove it from onDeserialize()
	local adjustHiringCostBasedOnEquipment = o.adjustHiringCostBasedOnEquipment;
	o.adjustHiringCostBasedOnEquipment = function () {};

	o.adjustHiringCostBasedOnEquipmentLegends <- function () {
		adjustHiringCostBasedOnEquipment();
	}

	local addEquipment = o.addEquipment;
	o.addEquipment = function () {
		addEquipment();
		adjustHiringCostBasedOnEquipmentLegends();
	}
	// endhack
})
