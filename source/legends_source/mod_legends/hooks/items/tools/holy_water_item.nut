::mods_hookExactClass("items/tools/holy_water_item", function(o) {
	o.m.OriginalValue <- null;
	o.m.OriginalDescription <- null;

	local create = o.create;
	o.create = function()
	{
		create();
		this.m.Description = "A flask filled with water blessed by a man of the gods. Can be thrown at short ranges. If the company has Alchemy Tools, this item is refilled after each battle, consuming 30 ammunition per use.";
		this.m.OriginalDescription = this.m.Description;
		this.m.Value = 800;
		this.m.OriginalValue = this.m.Value;
		this.m.ItemType = this.Const.Items.ItemType.Ammo | this.Const.Items.ItemType.Tool;
		this.m.Ammo = 1;
		this.m.AmmoMax = 1;
		this.m.AmmoCost = 30;
		this.m.RangeMax = 3;
	}

	o.getTooltip = function()
	{
		local result = [
			{
				id = 1,
				type = "title",
				text = this.getName()
			},
			{
				id = 2,
				type = "description",
				text = this.getDescription()
			}
		];

		if (this.getIconLarge() != null)
		{
			result.push({
				id = 3,
				type = "image",
				image = this.getIconLarge(),
				isLarge = true
			});
		}
		else
		{
			result.push({
				id = 3,
				type = "image",
				image = this.getIcon()
			});
		}

		result.extend([
		{
			id = 66,
			type = "text",
			text = this.getValueString()
		},
		{
			id = 64,
			type = "text",
			text = "Worn in Offhand"
		},
		{
			id = 7,
			type = "text",
			icon = "ui/icons/vision.png",
			text = "Range of [color=%positive%]" + this.m.RangeMax + "[/color] tiles"
		},
		{
			id = 4,
			type = "text",
			icon = "ui/icons/regular_damage.png",
			text = "Damage of [color=%damage%]20[/color] for [color=%damage%]3[/color] turns to any undead target hit"
		},
		{
			id = 5,
			type = "text",
			icon = "ui/icons/special.png",
			text = "Has a [color=%damage%]33%[/color] chance to hit bystanders at the same or lower height level as well"
		},
		{
			id = 9,
			type = "text",
			icon = "ui/icons/direct_damage.png",
			text = "[color=%damage%]100%[/color] of damage ignores armor"
		},
		{
			id = 10,
			type = "text",
			icon = "ui/icons/special.png",
			text = "Will remove [color=%status%]Battle Forged[/color] from Wichts"
		}]);
		if (!this.World.Retinue.hasFollower("follower.alchemist"))
		{
			result.push({
				id = 6,
				type = "text",
				icon = "ui/icons/warning.png",
				text = "Cannot be refilled after battle, because this company has no Alchemy Tools"
			});
		}
			return result;
	}

	o.consumeAmmo <- function()
	{
		m.AmmoCost = 0;
		weapon.consumeAmmo(); // to prevent scavenger retinue from recover ammo part
		m.AmmoCost = 30;
	}

	o.isAmountShown <- function ()
	{
		return true;
	}

	o.getAmountString <- function ()
	{
		return this.m.Ammo + "/" + this.m.AmmoMax;
	}

	o.getAmmo <- function() // prevent item from being refilled without the retinue
	{
		return m.Ammo == 0 && !this.World.Retinue.hasFollower("follower.alchemist") ? m.AmmoMax + 1 : m.Ammo;
	}

	o.setAmmo <- function ( _a )
	{
		this.weapon.setAmmo( _a );

		if (this.m.Ammo > 0)
		{
			this.m.Name = "Holy Water";
			this.m.IconLarge = "tools/holy_water_01.png";
			this.m.Icon = "tools/holy_water_01_70x70.png";
			this.m.ShowArmamentIcon = true;
			this.m.Description = this.m.OriginalDescription;
			this.m.Value = m.OriginalValue;
		}
		else
		{
			this.m.Name = "Holy Water (Used)";
			this.m.IconLarge = "tools/holy_water_01.png";
			this.m.Icon = "tools/holy_water_01_70x70.png";
			this.m.ShowArmamentIcon = false;
			this.m.Description = "A spent and shattered flask of holy water. If the company has \"Alchemy Tools\", this item is refilled after each battle, consuming 30 ammunition per use.";
			this.m.Value = 0;
		}

		this.updateAppearance();
	}

	o.onPutIntoBag <- function ()
	{
		local skill = ::Legends.Actives.get(this, ::Legends.Active.LegendLaunchHolyWater);
		if (skill != null && this.getAmmo() > 0)
			skill.setItem(this);
	}

	o.onSlingUpdateProperties <- function ()
	{
		this.onPutIntoBag();
	}

	o.onRemovedFromBag <- function()
	{
		this.item.onRemovedFromBag();
		local skill = ::Legends.Actives.get(this, ::Legends.Active.LegendLaunchHolyWater);
		if (skill != null)
			skill.setItem(null);
	}
});
