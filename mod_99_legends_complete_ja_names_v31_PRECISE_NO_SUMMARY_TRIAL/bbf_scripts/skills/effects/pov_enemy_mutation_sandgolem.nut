this.pov_enemy_mutation_sandgolem <- this.inherit("scripts/skills/skill", {
	m = {
		HeadArmorBoost = 100,
		HeadDamageTaken = 0,
		BodyArmorBoost = 100,
		BodyDamageTaken = 0
	},
	function create()
	{
		this.m.ID = "effects.pov_enemy_mutation_sandgolem";
		this.m.Name = "[color="+ ::Const.UI.Color.povEnemyMutation + "] Ifrit\'s Durability [/color]";
		this.m.Description = "This enemy has some properties of an Ifrit! So...rock";
		this.m.Icon = "skills/pov_ifrit_mutant.png";
		this.m.IconMini = "pov_mini_ifrit_mutant";
		this.m.Type = this.Const.SkillType.StatusEffect;
		this.m.IsActive = false;
		this.m.IsRemovedAfterBattle = true;
	}

	function getDescription()
	{
		return "This enemy has some properties of an Ifrit! So...rock";
	}

	function getTooltip()
	{
		local ret = [
			{
				id = 1,
				type = "title",
				text = this.getName()
			},
			{
				id = 2,
				type = "description",
				text = this.getDescription()
			},
			{
				id = 11,
				type = "text",
				icon = "ui/icons/armor_body.png",
				text = "This character\'s skin is hard and stone-like, granting [color=" + this.Const.UI.Color.PositiveValue + "]100[/color] points of natural armor"
			},
			{
				id = 11,
				type = "text",
				icon = "ui/icons/armor_body.png",
				text = "This character also gets [color=" + this.Const.UI.Color.PositiveValue + "]15%[/color]Damage Reduction."
			},
			{
				id = 11,
				type = "text",
				icon = "ui/icons/initiative.png",
				text = "Lose [color=" + this.Const.UI.Color.NegativeValue + "]30%[/color] Initiative."
			},
			{
				id = 11,
				type = "text",
				icon = "ui/icons/fatigue.png",
				text = "All skills now cost [color=" + this.Const.UI.Color.NegativeValue + "]15%[/color] more Fatigue."
			},
			{
				id = 11,
				type = "text",
				icon = "ui/icons/fatigue.png",
				text = "Fatigue recovery is reduced by [color=" + this.Const.UI.Color.NegativeValue + "]3[/color] Points."
			}

		];
		return ret;
	}

	function onAdded() 
	{
		local actor = this.getContainer().getActor();
		if (!::MSU.isKindOf(actor, "player")) 
		{
			::TLW.MutagenDrop.addMutagenDrop(actor, ::TLW.Mutation.Sandgolem);
	  	}

	  	if (!this.m.Container.hasSkill("actives.pov_throw_dirt"))
		{
			local charge = this.new("scripts/skills/actives/pov_throw_dirt_skill");
			//charge.m.ActionPointCost = 5;
			//charge.m.FatigueCost = 30;
			//charge.m.MaxRange = 3;
			this.m.Container.add(charge);

			if (actor.getAIAgent().findBehavior(::Const.AI.Behavior.ID.Distract) == null)
			{
				actor.m.AIAgent.addBehavior(this.new("scripts/ai/tactical/behaviors/ai_distract"));
				actor.getAIAgent().finalizeBehaviors();
			}
		}
	}

	function onCombatStarted()
	{
		this.m.HeadDamageTaken = 0;
		this.m.BodyDamageTaken = 0;
	}

	function onCombatFinished()
	{
		this.m.HeadDamageTaken = 0;
		this.m.BodyDamageTaken = 0;
	}

	function onUpdate( _properties )
	{
		// BUFFS
		_properties.Armor[this.Const.BodyPart.Head] += this.Math.max(0.0, this.m.HeadArmorBoost - this.m.HeadDamageTaken);
		_properties.Armor[this.Const.BodyPart.Body] += this.Math.max(0.0, this.m.BodyArmorBoost - this.m.BodyDamageTaken);
		_properties.ArmorMax[this.Const.BodyPart.Head] += this.m.HeadArmorBoost;
		_properties.ArmorMax[this.Const.BodyPart.Body] += this.m.BodyArmorBoost;
		// DEBUFFS
		_properties.Initiative *= 0.70;
		_properties.StaminaMult *= 0.90;
		_properties.FatigueEffectMult *= 1.10;
		_properties.FatigueRecoveryRate += -2;
	}

	function onBeforeDamageReceived( _attacker, _skill, _hitInfo, _properties )
	{
		if(_properties.YrdenTrapped == false)
		{
			_properties.DamageReceivedTotalMult *= 0.85;
		}
		
		if (_hitInfo.BodyPart == this.Const.BodyPart.Head)
		{
			if (this.m.HeadDamageTaken >= this.m.HeadArmorBoost)
			{
				return;
			}

			// weird fix to weird issue
			local cap = this.m.HeadArmorBoost - this.m.HeadDamageTaken;
			if (cap > this.m.HeadArmorBoost/2)
			{
				cap = this.m.HeadArmorBoost/2;
			}
			_properties.DamageArmorReduction += cap;
			this.m.HeadDamageTaken += _hitInfo.DamageArmor;
		}
		else if (_hitInfo.BodyPart == this.Const.BodyPart.Body)
		{
			if (this.m.BodyDamageTaken >= this.m.BodyArmorBoost)
			{
				return;
			}

			// weird fix to weird issue
			local cap = this.m.BodyArmorBoost - this.m.BodyDamageTaken;
			if (cap > this.m.BodyArmorBoost/2)
			{
				cap = this.m.BodyArmorBoost/2;
			}
			_properties.DamageArmorReduction += cap;
			this.m.BodyDamageTaken += _hitInfo.DamageArmor;
		}
	}
});
